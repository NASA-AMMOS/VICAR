	INCLUDE 'VICMAIN_FOR'
	SUBROUTINE MAIN44
C
	INTEGER IHIST(256,256),ISORT(65536)
	INTEGER*2 IN1(30000),IN2(30000)
	LOGICAL XVPTST,RESCALE,ROOT_SCALE
	CHARACTER*80 PRT
	CHARACTER*4 FORMAT
C								open inputs
	CALL XVUNIT(IUNIT1,'INP',1,ISTAT,' ')
	CALL XVOPEN(IUNIT1,ISTAT,'OPEN_ACT','SA','IO_ACT','SA',
     +		    'U_FORMAT','HALF',' ')
	CALL XVUNIT(IUNIT2,'INP',2,ISTAT,' ')
	CALL XVOPEN(IUNIT2,ISTAT,'OPEN_ACT','SA','IO_ACT','SA',
     +		    'U_FORMAT','HALF',' ')
C								get parameters
	CALL XVPARM('LINC',INCL,ICOUNT,IDEF,0)
	CALL XVPARM('SINC',INCS,ICOUNT,IDEF,0)
	CALL XVPARM('INC',I,ICOUNT,IDEF,0)
	IF (IDEF.EQ.0) THEN
	    INCL = I
	    INCS = I
	END IF
	RESCALE = .NOT.XVPTST('NONE')
	IF (RESCALE) THEN
	    ROOT_SCALE = XVPTST('ROOT')
	    CALL XVPARM('SPIKES',NORM,ICOUNT,IDEF,0)
	    FORMAT = 'BYTE'
	ELSE
	    FORMAT = 'FULL'
	END IF
	CALL XVSIZE(ISL,ISS,NL,NS,NLIN,NSIN)
	IEL = ISL+NL-1
C								open output
	CALL XVUNIT(IOUTUNIT,'OUT',1,ISTAT,' ')
	CALL XVOPEN(IOUTUNIT,ISTAT,'U_FORMAT','FULL','O_FORMAT',FORMAT,
     &		    'OP','WRITE','U_NL',256,'U_NS',256,
     &		    'OPEN_ACT','SA','IO_ACT','SA',' ')
C								initialize hist
	DO I=1,256
	    DO J=1,256
		IHIST(I,J) = 0
	    END DO
	END DO
C								form histogram
	DO I=ISL,IEL,INCL
	    CALL XVREAD(IUNIT1,IN1,ISTAT,'LINE',I,'SAMP',ISS,
     &			'NSAMPS',NS,' ')
	    CALL XVREAD(IUNIT2,IN2,ISTAT,'LINE',I,'SAMP',ISS,
     &			'NSAMPS',NS,' ')
	    DO J = 1,NS,INCS
		IHIST(IN1(J)+1,IN2(J)+1) = IHIST(IN1(J)+1,IN2(J)+1) + 1
	    END DO
	END DO
C							do rescaling
C
	IF (RESCALE) THEN
	    CALL MVE(4,65536,IHIST,ISORT,1,1)		! find the normalizing
	    CALL SORTIN(ISORT,65536)			! bin by sorting;
	    X = ISORT(65537-NORM)			! store normalizing
	    WRITE (PRT,100) X				! constant in X
  100	    FORMAT(' Output Normalized to',F9.1)
	    CALL XVMESSAGE(PRT,' ')
	    IF (ROOT_SCALE) THEN			! rescale using sqrt
		DO I=1,256
		    DO J=1,256
			IHIST(I,J) = MIN(255,
     &				        NINT(255.0*SQRT(IHIST(I,J)/X)))
		    END DO
		END DO
	    ELSE					! linear rescaling
		DO I=1,256
		    DO J=1,256
			IHIST(I,J) = MIN(255,NINT(255.0*IHIST(I,J)/X))
		    END DO
		END DO
	    END IF
	END IF
C							write output, reversing
C							the order of the lines
	DO I=256,1,-1
	    CALL XVWRIT(IOUTUNIT,IHIST(1,I),ISTAT,'NSAMPS',256,' ')
	END DO
	RETURN
	END
