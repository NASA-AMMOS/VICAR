      INCLUDE 'VICMAIN_FOR'
C
C  91-04-24   ...REA... CONVERT TO UNIX/VICAR
C  87-12-01   ...REA..  FIX BUG IN BSQ FORMAT PROCESSING
C  84-11-26   ...LWK... CONVERTED TO VICAR2
C MODIFIED FOR VAX CONVERSION BY ASM, 26 AUG 1983
C
      SUBROUTINE MAIN44
C
      IMPLICIT INTEGER (A-Z)
      INTEGER*4 NLI(10), NSI(10), IUNIT(10), OUNIT, NBANDS(10)
      CHARACTER*4 FMT(4)/ 'BYTE', 'HALF', '    ', 'FULL'/, RFMT/ 'REAL'/
      CHARACTER*4 FMT0
      BYTE BUF(100000)
      CHARACTER*80 PRT
      CHARACTER*4 CFMT(4)/ 'BYTE', 'HALF', '    ', 'FULL'/,
     &            FCOD
      CHARACTER*3 ORG(10)
C
C						        GET NUMBER OF INPUTS
      CALL XVPCNT( 'INP', NI)
C
C READ ALL VICAR LABELS TO FIND SIZE OF OUTPUT PIC
C SAVE THE NUMBER OF LINES AND SAMPLES IN EACH INPUT PIC

      NLINE = 0
      ISAMP = 0
      NSAMP = 0
      FLAG = 0		! 0 = UNKNOWN, 1 = INTEGER, 2 = REAL, 3 = MIXED
      BPS = 4		! LARGEST POSSIBLE PIX_SIZE (CURRENTLY)

      DO 300 I=1,NI
      CALL XVUNIT( IUNIT(I), 'INP', I, STAT, ' ')
      CALL XVOPEN( IUNIT(I), STAT, 'OPEN_ACT', 'SA', 'IO_ACT', 'SA',' ')
      CALL XVGET( IUNIT(I), STAT, 'NL',NLI(I), 'NS',NSI(I), 'PIX_SIZE',
     &         IBYTES, 'FORMAT', FCOD, 'NB',NBANDS(I), 'ORG',ORG(I),' ')
      IF ((FCOD.EQ.'REAL' .AND. FLAG.EQ.1) .OR.
     &	  (FCOD.NE.'REAL' .AND. FLAG.EQ.2)) THEN
	CALL XVMESSAGE('** WARNING: REALS MIXED WITH INTEGERS **',' ')
	FLAG = 3
      ELSEIF (FLAG.NE.3) THEN
	IF (FCOD.EQ.'REAL') THEN
	  FLAG = 2
	ELSE
	  FLAG = 1
	ENDIF
      ENDIF
      NLINE = NLINE+NLI(I)
      NBYT = MAX0( NBYT, NSI(I)*IBYTES)
      BPS = MIN0( BPS, IBYTES)
      NSAMP = MAX0( NSAMP, NBYT/BPS)
  300 CONTINUE
C
C			   CHECK THAT ORGANIZATIONS AND NUMBER OF BANDS ARE SAME
      DO I=2,NI
         IF(ORG(I).NE.ORG(1)) THEN
	    CALL XVMESSAGE(
     +            ' ** ALL INPUTS MUST HAVE SAME ORGANIZATION **',' ')
	    CALL ABEND
	 ENDIF
         IF(NBANDS(I).NE.NBANDS(1)) THEN
	    CALL XVMESSAGE(
     +            ' ** ALL INPUTS MUST HAVE SAME NUMBER BANDS **',' ')
	    CALL ABEND
	 ENDIF
         IF(ORG(1).EQ.'BIP' .AND. NSI(I).NE.NSI(1)) THEN
	    CALL XVMESSAGE(
     + ' FOR BIP ORGANIZATION ALL INPUTS MUST HAVE SAME NUMBER SAMPS',
     + ' ')
	    CALL ABEND
	 ENDIF
      END DO
C         
      NB=NBANDS(1)
C
C OUTPUT FORMAT MAY NOT BE REAL IN MIXED CASE, TO AVOID RESERVED FLOATING
C OPERAND EXCEPTIONS.
C
      NWORDS=(NBYT-1)/4+1
      FMT0 = FMT(BPS)
      IF (FLAG.EQ.2) FMT0 = RFMT
      WRITE (PRT,400) NLINE,NSAMP,FMT0
  400 FORMAT(' OUTPUT: NL=',I6,', NS=',I6,', FORMAT=',A4,'.')
      CALL XVMESSAGE(PRT,' ')
C
C							    OPEN OUTPUT DATA SET
      CALL XVUNIT( OUNIT, 'OUT', 1, STAT, ' ')
      FCOD = CFMT(BPS)
      IF (FLAG.EQ.2) FCOD = 'REAL'
      CALL XVOPEN( OUNIT, STAT, 'OP', 'WRITE', 'U_NL', NLINE, 'U_NS',
     &             NSAMP, 'O_FORMAT', FCOD, 'U_FORMAT', FCOD,
     &            'U_NB',NB,'U_ORG',ORG(1),
     &            'OPEN_ACT', 'SA', 'IO_ACT', 'SA', ' ')

C					       PASTE TOGETHER THE OUTPUT DATASET
      IF (ORG(1).EQ.'BSQ') THEN
	 DO IBAND=1,NB
	    DO I=1,NI
               DO ILINE=1,NLI(I)
                  CALL ZIA(BUF,NWORDS)
          	  CALL XVREAD(IUNIT(I),BUF,STAT,
     &                        'BAND',IBAND,'LINE',ILINE,' ')
     	          CALL XVWRIT(OUNIT,BUF,STAT,' ')
               END DO
            END DO
	 END DO
      ELSE IF(ORG(1) .EQ. 'BIL') THEN
	 DO I=1,NI
            DO ILINE=1,NLI(I)
               DO IBAND=1,NB
                  CALL ZIA(BUF,NWORDS)
         	  CALL XVREAD(IUNIT(I),BUF,STAT,
     &                        'BAND',IBAND,'LINE',ILINE,' ')
    	          CALL XVWRIT(OUNIT,BUF,STAT,' ')
               END DO
            END DO
	 END DO
      ELSE IF(ORG(1) .EQ. 'BIP') THEN
	 DO I=1,NI
            DO ILINE=1,NLI(I)
               DO ISAMP=1,NSI(I)
                 CALL ZIA(BUF,NWORDS)
       	         CALL XVREAD(IUNIT(I),BUF,STAT,'BAND',1,'NBANDS',NB,
     &                       'SAMP',ISAMP,'LINE',ILINE,' ')
  	         CALL XVWRIT(OUNIT,BUF,STAT,' ')
               END DO
            END DO
	 END DO
      END IF
C
C							        CLOSE DATA SETS
      DO I=1,NI
         CALL XVCLOSE( IUNIT(I), STAT, ' ')
      END DO
      CALL XVCLOSE( OUNIT, STAT, ' ')
C
      RETURN
      END
