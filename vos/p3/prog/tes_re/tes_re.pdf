! VICAR procedure to do calculate tes emissivity by iteration

PROCEDURE help=*

! TIMSCAL2 allowed input parameters
parm  INP	(string,40)
parm  OUT       (string,40)
parm  SL	integer	default=1
parm  NL	integer	default=0
parm  DATE	integer default=-1
parm  HEIGHT	real	default=10.0
parm  DATUM	real	default=0.001
parm  AMODEL	string	valid=(TR,MS,MW,SS,SW,ST,RS)
parm  ATEMP	string	default="  "	valid=(TR,MS,MW,SS,SW,ST,"  ")
parm  AHUMID	string	default="  "	valid=(TR,MS,MW,SS,SW,ST,"  ")
parm  AOZONE	string	default="  "	valid=(TR,MS,MW,SS,SW,ST,"  ")
parm  NLAYERS	integer	default=0
parm  WTYPE	keyword	default=RH	valid=(DP,RH,DE)
parm  ALTITUDE	real	count=0:34	default=0.0
parm  PRESSURE	real	count=0:34	default=0.0
parm  TEMP	real	count=0:34	default=0.0
parm  WATER	real	count=0:34	default=0.0
parm  WATERFAC  real                    default=1.0
parm  O3FAC	real	                default=1.0
parm  CO2FAC	real	                default=1.0
parm  SO2FAC	real	                default=1.0
parm  CH4FAC	real	                default=1.0
parm  O2FAC	real	                default=1.0
parm  E		real	default=(0.95,0.95,0.95,0.95,0.95,0.95)	count=6
parm  ATMODEL   keyword default=MODTRAN valid=(LOWTRAN,MODTRAN)

! TES allowed input parameters
parm    WAVE    type=real count=(1:6)
parm    NTESITER   type=integer default=8

! TITER allowed input paramters
parm    NSKYITER type=integer default=1

! LOCAL variables
LOCAL ETEMPORARY type=string
LOCAL GTEMPORARY type=string
LOCAL COUNTER    type=integer
LOCAL DEFAULT    type=string

BODY

! Set the output file name
LET etemporary = out // ".1"
! Set the temporary grad file
LET gtemporary = out // ".temporary"

! Call TIMSCAL2 once with assumed emissivities in the 6 TIMS channels to
! get a GRAD image.
timscal2 inp=&inp out=&gtemporary sl=&sl nl=&nl date=&date +
  height=&height datum=&datum calmode="GRAD" amodel=&amodel +
  atemp=@atemp ahumid=@ahumid aozone=@aozone +  
  nlayers=&nlayers wtype=&wtype altitude=&altitude  + 
  pressure=&pressure temp=&temp water=&water waterfac=&waterfac + 
  o3fac=&o3fac co2fac=&co2fac so2fac=&so2fac ch4fac=&ch4fac +
  o2fac=&o2fac e=&e atmodel=&atmodel
    
! Call tes once to get an emissivity image to use with TIMSCAL2RE
tes inp=&gtemporary out=&etemporary wave=&wave emis=&e + 
  niter=&ntesiter

! Set the counters
LET counter=1
LET etemporary = out // ".&counter"
   
LOOP
  IF (counter >= nskyiter) BREAK
  LET counter=counter+1
 
! Correct to ground radiance
  timscal2re inp=(&inp,&etemporary) out=&gtemporary + 
    sl=&sl nl=&nl date=&date height=&height datum=&datum +
    amodel=&amodel atemp=@atemp ahumid=@ahumid aozone=@aozone + 
    nlayers=&nlayers wtype=&wtype altitude=&altitude +
    pressure=&pressure temp=&temp water=&water waterfac=&waterfac + 
    o3fac=&o3fac co2fac=&co2fac so2fac=&so2fac ch4fac=&ch4fac + 
    o2fac=&o2fac atmodel=&atmodel
 
  LET etemporary = out // ".&counter"
  
 write ""
 write "Iteration Number &counter"
 write ""

! Extract the emissivity  
  tes inp=&gtemporary out=&etemporary wave=&wave emis=&e +
    niter=&ntesiter
   
END-LOOP

! Delete the temporary grad file
ush \rm &gtemporary

END-PROC
.HELP
Program to calculate TES emissivity by iteration.  
.LEVEL1
.VARIABLE INP
Input radiance at sensor 
dataset (Halfword) in 
radiance (mW/m*m/um/sr)
.VARIABLE OUT
Root name for output. Root 
will have ".1", ".2" etc 
appended for each iteration. 
Output includes emissivity and 
an extra band of temperature. 
All output data in Halfword. 
Emissivity data *1.0e4. 
Temperature data degrees*100.
.VARI SL
Starting line
.VARI NL
Number of lines
.VARI AMODEL
Selects the model for which the
atmospheric corrections will
be performed via LOWTRAN/MODTRAN
.VARI ATEMP
Selects atmospheric temperature
and pressure profiles, if 
different from that defaulted
with AMODEL.
.VARI AHUMID
Selects the atmospheric water
vapor model, if different from
that defaulted with AMODEL.
.VARI AOZONE
Selects the ozone model, if
different from that defaulted
with AMODEL.
.VARI HEIGHT
Aircraft altitude (km above MSL)
.VARI DATUM
Surface elevation (km above MSL)
.VARI NLAYERS
The number of atmospheric lay-
ers in the radiosonde data
(if used).
.VARI ALTITUDE
The altitudes of each of the
atmospheric layers given in
the radiosonde data. (km.)
.VARI PRESSURE
The atmospheric pressures of
each of the atmospheric lay-
ers given in the radiosonde
data. (mb.)
.VARI TEMP
The temperatures of the at-
mospheric layers given in
the radiosonde data. (deg C)
.VARI WATER
The water profile in terms
of the parameter WTYPE, for
each layer in the radiosonde
data.
.VARI WTYPE
The units in which H2OS are
given.  The three types are
relative humidity (RH), dew-
point (DP), and density (DE)
.VARI WATERFAC
Varies the moisture profile
by the factor specified
.VARI O3FAC
Varies the ozone profile by
the factor specified
.VARI CO2FAC
Varies the carbon dioxide
profile by the factor specified
.VARI SO2FAC
Varies the sulfur dioxide
profile by the factor specified
.VARI CH4FAC
Varies the methane profile by
the factor specified
.VARI O2FAC
Varies the oxygen profile by
the factor specified
.VARI E
The emissivity assumed for
each band. 
.VARI DATE
Date of data acquisition. Used
to override the date in the
VICAR label (yymmdd)
.VARI ATMODEL
Atmospheric model to be used
(LOWTRAN or MODTRAN)
.VARIABLE WAVE
the central wavelength for the 
input channels, in micrometers.
(1 value per channel)
.VARIABLE NTESITER
Number of TES iterations.
.VARIABLE NSKYITER
Number of atmospheric
iterations.
.LEVEL2
.VARIABLE WAVE
The centroid wavelength value for each channel in micrometers. This can be 
calculated for a TIMS image using TIMSRESP (The central wavelength values are
dumped to the screen after the program runs).
.END




