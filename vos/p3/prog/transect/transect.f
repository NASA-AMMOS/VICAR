	INCLUDE 'VICMAIN_FOR'
	SUBROUTINE MAIN44
C -------------------------------------------------------------
C  13 JUL 89    REA   NEW PROGRAM
C  25 APR 90    REA   INCLUDE AXISPTS, AXISPTS2 SUBROUTINES
C  18 SEP 90    REA   ADD LASER PLOTTING CAPABILITY
C  12 MAY 92    REA   ADD 'PRINT OPTION
C  12 JUL 96    REA   CONVERT FROM REGIS TO IDL USAGE
C
	REAL X(40000),Y(40000),BUF(40000)
	INTEGER IBUF(2)
	LOGICAL XVPTST,QPRINT
	CHARACTER*80 TITLE,MSG
	CHARACTER*48 TITLE2
C
C     PROCESS THE PARAMETERS
C
	CALL XVUNIT(INUNIT,'INP',1,ISTAT,' ')
	CALL XVOPEN(INUNIT,ISTAT,'U_FORMAT','REAL','OPEN_ACT','SA',
     +              'IO_ACT','SA',' ')
C                                                     START
	CALL XVPARM('START',IBUF,ICNT,IDEF,0)
	ISL = IBUF(1)
	ISS = IBUF(2)
C						      END
	CALL XVPARM('END',IBUF,ICNT,IDEF,0)
	IEL = IBUF(1)
	IES = IBUF(2)
C						      BAND
	CALL XVPARM('BAND',IBAND,ICNT,IDEFBAND,0)	
	IF (IDEFBAND.EQ.0) THEN
	    WRITE (TITLE2,100) ISL,ISS,IEL,IES,IBAND,CHAR(0)
  100	    FORMAT('From (',I5,',',I5,') to (',I5,',',I5,') in Band',
     +		   I4,A1)
	ELSE
	    WRITE (TITLE2,200) ISL,ISS,IEL,IES,CHAR(0)
  200	    FORMAT('From (',I5,',',I5,') to (',I5,',',I5,')',A1)
	END IF
C						      YAXIS
	CALL XVPARM('YAXIS',X,ICNT,IDEF,0)
	YLO = X(1)
	YHI = X(2)
C							PRINT
	QPRINT = XVPTST('PRINT')
	IF (QPRINT) CALL XVMESSAGE(
     +		'      Line    Sample   Distance       DN',' ')
C							TITLE
	CALL XVP('TITLE',TITLE,ICNT)
	CALL ADD0(TITLE,80,LEN)
C
	DX = ABS(IES-ISS)
	DY = ABS(IEL-ISL)
	IF (DY .GE. DX) THEN				! mostly vertical
	    XINC = SQRT(DX*DX + DY*DY)/DY
	    IF (IEL .GT. ISL) THEN
		INC = 1
	    ELSE
		INC = -1
	    END IF
	    N = 0
	    DO I=ISL,IEL,INC
		FRAC = ABS(I-ISL)/DY
		ISAMP = (1.0-FRAC)*ISS + FRAC*IES + 0.5
		N = N+1
		CALL XVREAD(INUNIT,Y(N),ISTAT,'NSAMPS',1,'SAMP',ISAMP,
     +			    'LINE',I,'BAND',IBAND,' ')
		X(N) = (N-1)*XINC
		IF (QPRINT) THEN
		    WRITE (MSG,500) I,ISAMP,X(N),Y(N)
  500		    FORMAT(2I10,F10.3,F15.4)
 		    CALL XVMESSAGE(MSG,' ')
 		END IF
	    END DO
	ELSE						! mostly horizontal
	    XINC = SQRT(DX*DX + DY*DY)/DX
	    IF (IES .GT. ISS) THEN
		INC = 1
	    ELSE
		INC = -1
	    END IF
	    N = 0
	    LLINE = 0
	    DO I=ISS,IES,INC
		FRAC = ABS(I-ISS)/DX
		LINE = (1.0-FRAC)*ISL + FRAC*IEL + 0.5
		IF (LINE .NE. LLINE) CALL XVREAD(INUNIT,BUF,ISTAT,
     +			    		'LINE',LINE,'BAND',IBAND,' ')
		LLINE = LINE
		N = N+1
		X(N) = (N-1)*XINC
		Y(N) = BUF(I)
	 	IF (QPRINT) THEN
		    WRITE (MSG,500) LINE,I,X(N),Y(N)
		    CALL XVMESSAGE(MSG,' ')
		END IF
	    END DO
	END IF
C
	CALL PLOTXY(X,Y,N,YLO,YHI,INUNIT,TITLE,TITLE2)
	RETURN
	END
C
C****************************************************************************
C
	SUBROUTINE PLOTXY(XBUF,YBUF,NUM,Y1,Y2,INUNIT,TITLE,TITLE2)
C
	REAL XBUF(NUM),YBUF(NUM)
	CHARACTER*80 TITLE
	CHARACTER*48 TITLE2
	CHARACTER*40 XLABEL,YLABEL,MSG
	LOGICAL QPSPLOT
C								scale x-axis
	CALL MINMAX(7,NUM,XBUF,X1,X2,IMIN,IMAX)
	IF (X1.EQ.X2) THEN
	    CALL XVMESSAGE(
     +		' More than 1 value is needed for plotting.',' ')
	    CALL ABEND
	ENDIF
	CALL AXISPTS(X1,X2,XLO,XHI,NXTIC)
C								scale Y-axis
	IF (Y1.EQ.Y2) THEN
	    CALL MINMAX(7,NUM,YBUF,Y1,Y2,IMIN,IMAX)
	    IF (Y1.EQ.Y2) Y2 = Y1+1
	    CALL AXISPTS(Y1,Y2,YLO,YHI,NYTIC)
	ELSE
	    CALL AXISPTS2(Y1,Y2,NYTIC)
	    YLO = Y1
	    YHI = Y2
	END IF
C								   plot the data
	OPEN (11,FILE='scrvtransect001',STATUS='NEW')
	WRITE (11,100) XLO,XHI,YLO,YHI
  100	FORMAT(4E16.7)
	WRITE (11,200) NXTIC,NYTIC
  200	FORMAT(2I10)
	DO I=1,NUM
	    WRITE (11,100) XBUF(I),YBUF(I)
	END DO
	CLOSE(11)
	ISTAT = SYSTEM('idl vicartran.inp')
	OPEN (11,FILE='scrvtransect001',STATUS='OLD')
	CLOSE (11,STATUS='DELETE')
C
	CALL XVINTRACT('IPARAM','Print this plot (Yes, No) [No]? ')
	CALL XVIPARM('PENPLOT',MSG,ICNT,IDEF,0)
	QPSPLOT = MSG(1:1).EQ.'Y' .OR. MSG(1:1).EQ.'y'
C							submit to the printer
	IF (QPSPLOT) THEN
	    CALL XVGET(INUNIT,ISTAT,'NAME',MSG,' ')
	    CALL ADD0(MSG,40,LEN)
	    CALL XVPARM('XLABEL',XLABEL,ICNT,IDEF,0)
	    CALL ADD0(XLABEL,40,LEN)
	    CALL XVPARM('YLABEL',YLABEL,ICNT,IDEF,0)
	    CALL ADD0(YLABEL,40,LEN)
	    CALL PSPLOT(XBUF,YBUF,NUM,XLO,XHI,YLO,YHI,NXTIC,NYTIC,
     +			XLABEL,YLABEL,TITLE,MSG,TITLE2,-1)
	END IF
	RETURN
	END
