procedure help=*

parm inp (string,60) count=1:10 
parm out (string,60) count=1:10 
parm wts type=integer default=25
parm direct type=keyword default=horiz valid=(horiz,vert) 
local icnt integer   initial=1
local xx (string,60) initial=" "
local yy (string,60) initial=" "
local zz type=string  initial="'horiz"
local fmt type=string
local nl type=integer
local ns type=integer
local nb type=integer

body

if (direct="vert") let zz="'vert "
loop
   if (icnt>$count(inp)) break
   let xx=inp(&icnt)
   let yy=out(&icnt)
   write "   "
   write "CLEANING FILE &icnt"
   form &xx fmt nl ns nb
   if (fmt="BYTE")
      let fmt="128"
   else
      let fmt="0"
   end-if
   lave &xx CLEANUPSCR &zz 'high 'image 'noprint filter=&wts
   F2 (&xx,CLEANUPSCR) &yy func="IN1-IN2+&fmt"
   let icnt=icnt+1
end-loop
ush \rm CLEANUPSCR
end-proc

.title VICAR2 Procedure CLEANUP
.HELP
CLEANUP is a VICAR procedure designed to clean line noise from
scanner data. The procedure will clean up to ten single-band
images in a single run. 

CLEANUP works by adjusting each line (in the VERT mode) or each column (in
the HORIZ mode) by adding a constant to that line/sample, so that its
average matches that of the surrounding WTS lines/samples.

CLEANUP can be run in either a horizontal (cleaning rows) or vertical (cleaning
columns) mode.  It may be run on BYTE, HALF, FULLWORD, or REAL data formats,
but it will only run on files containing a single channel.

.LEVEL1
.variable inp
names of the 
images to be cleaned.
.variable out
names for the cleaned
versions of input images.
.variable wts
Number of weights for
the high-pass filter
(must be an odd integer)
.variable direct
Direction of averaging.
Valid: horiz, vert
.level2
.variable inp
The user may input up to ten files. BYTE, HALF, FULL, and REAL pixel formats
are accepted, but each file must contain only a single channel.
.variable out
The name(s) of the output (cleaned) files.  There should be one output file
for each input file.1
.variable wts
The user may specify the number of
weights for the high-pass filter that
is run over the averaged image (see the
general help). The spatial frequency passed
by the filter is inversely proportional to 
the length of the filter. 
.variable direct
This variable is the direction of averaging. Use HORIZ to remove horizontal
striping, and use VERT to remove vertical striping.
.end
