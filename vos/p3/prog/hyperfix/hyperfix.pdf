Procedure help=*

parm inp	(string,80)
parm out	(string,80)
parm sl		integer default=1
parm nl		integer default=0
local i		integer	initial=71
local j		integer
local nli       integer
local offset	real
refgbl $echo

Body

let $echo="yes"

if (nl = 0) 
  FORM &inp NL=nli
else
  let nli=nl
end-if

COPY &inp &out NS=255 NL=&nli SL=&sl
GEN SCR990229SAMP NL=&nli NS=255 'REAL LINC=0.0 SINC=1.0 IVAL=2.0

let offset = $FLOAT(sl) + 0.5
loop
  COPY &inp SCR990229RAW SB=&i NB=1
  GEN SCR990229LINE NL=&nli NS=255 'REAL LINC=1.0 SINC=0.0 IVAL=&offset
  TRAN (SCR990229LINE,SCR990229SAMP) SCR990229MAP 'BIL
  TGEOM2 (SCR990229MAP,SCR990229RAW) SCR990229FIX
  INSERT3D (&out,SCR990229FIX) BAND=&i 'UPDATE 'OVER
  if (i = 242) break
  let offset = offset - 0.00877193
  let i = i + 1
end-loop
ush rm -f SCR990229*


End-proc

.TITLE
TAE PROCEDURE HYPERFIX
.HELP
PURPOSE:
 
   HYPERFIX is a VICAR procedure to correct for the misregistration of
the SWIR bands in HYPERION image data. It assumes that all SWIR channels
(Channels 71 - 242) are misregistered 1.0 pixel to the right, and the up-down
misregistration varies in evenly spaced increments from 0.5 pixels down for
Channel 71, to 1.0 pixels up for Channel 242.
 
EXECUTION:
 
   The following is the execution statement format for HYPERFIX:
 
		HYPERFIX INP OUT PARAMS
 
   where INP, OUT, and PARAMS are parameters discussed in their respective
parameter sections. 
.PAGE
OPERATION:
 
   HYPERFIX copies the input file into the output file, then for each of
Bands 71 through 242, it creates a rectification image using GEN and TRAN,
performs a registration using TGEOM2, and replaces that band into the output 
file, using INSERT3D.

   The user may process a subset of the image in the line direction, by
using the SL and NL parameters.  The last sample (256) is removed from
the output image, since there are no corresponding pixels to this sample in
the SWIR bands.  Otherwise, no subsetting is permitted in the sample direction.

   Since INSERT3D requires the input file to be in BSQ format, the input
to HYPERFIX must also be in BSQ format.
.PAGE 
EXAMPLES:
 
1) HYPERFIX INP OUT
 
2) HYPERFIX INP OUT SL=101 NL=500
 
 
WRITTEN BY:  Ron Alley     6 February 2002
 
COGNIZANT PROGRAMMER:  same
 
REVISION: NEW
.LEVEL1
.VARIABLE INP
input dataset name (BSQ format)
.VARIABLE OUT
output dataset name
.VARIABLE SL
first line of input to be used
.VARIABLE NL
# of lines of input to be used
(Default is entire input image)
.LEVEL2
.VARIABLE INP
The name of the input dataset. The input dataset must be in band sequential
(BSQ) format.
.VARIABLE OUT
The name of the output dataset.
.VARIABLE SL
The first line of input image to be output.
.VARIABLE NL
The number of lines of input image to be output.
.END
