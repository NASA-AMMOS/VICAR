PROCEDURE	HELP=*

PARM	inp	TYPE=(STRING,40)		! Input dataset name
PARM	out	TYPE=(STRING,40)  COUNT=(1:8)	! Output dataset name(s)
PARM	chan	TYPE=INTEGER COUNT=(1:8) DEFAULT=(1,2,3,4,5,6,7,8)
						! Channels to be logged
PARM	date	TYPE=(STRING,15)		! Date of flight
PARM	site	TYPE=(STRING,40)		! Site name
PARM	flight	TYPE=(STRING,20)		! Flight number
PARM	line	TYPE=(STRING,10)		! Flight line number
PARM    run     TYPE=(STRING,10)  DEFAULT=1	! Flight run number
PARM	text	TYPE=STRING  COUNT=(1:8)+
		DEFAULT=("NS001  CHANNEL  1 OF  8   0.45 TO 0.52 MICRONS",+
			 "NS001  CHANNEL  2 OF  8   0.52 TO 0.60 MICRONS",+
			 "NS001  CHANNEL  3 OF  8   0.63 TO 0.69 MICRONS",+
			 "NS001  CHANNEL  4 OF  8   0.76 TO 0.90 MICRONS",+
			 "NS001  CHANNEL  5 OF  8   1.00 TO 1.30 MICRONS",+
			 "NS001  CHANNEL  6 OF  8   1.55 TO 1.75 MICRONS",+
			 "NS001  CHANNEL  7 OF  8   2.08 TO 2.35 MICRONS",+
			 "NS001  CHANNEL  8 OF  8   10.4 TO 12.5 MICRONS")
LOCAL	i	TYPE=INTEGER			! Channel number
LOCAL	j	TYPE=INTEGER			! Starting byte of input
LOCAL	n	TYPE=INTEGER INITIAL=1		! Index
LOCAL	nmax	TYPE=INTEGER			! # of channels to be logged
LOCAL	txt	TYPE=STRING
LOCAL   outds	TYPE=STRING

BODY

LET nmax=$COUNT(chan)

LOOP						! Log each requested channel
   LET outds=out(n)
   LET i=chan(n)
   LET j=750*i-699
   LET txt=text(i)
   COPY &inp &outds SS=&j NS=699
   LABEL-ADD &outds ITEMS="BANDPASS='&txt' DATE='&date' SITE='&site'"
   LABEL-ADD &outds ITEMS="FLIGHT='&flight' LINE='&line' RUN='&run'"
   IF (n=nmax) BREAK
   LET n=n+1
END-LOOP

END-PROC
.TITLE 
VICAR2 Procedure NS1
.HELP
     NS1 is a logging procedure for data acquired from the NS001
multispectral scanner. The NS001 scanner is an 8 channel instrument with 7
bands in visible and near-IR and a single thermal-IR band. Its bandpasses are
similar to the LANDSAT-TM system, with an added band at 1.0 to 1.3 microns. The
NS001 scanner was designed to be operated from an aircraft platform, and scenes
are typically acquired from a C-130 operated by NASA AMES. 
.PAGE
     This TAE procedure separates the input data file into separate files for
each band logged, and adds the standard annotation for NS001 data.The user may 
choose to log all bands, or only a subset of the bands.
     The user is required to specify the following information when running
NS001:
	the input dataset name
	the output dataset(s)
	the flight number, line/run number, and date of the data acquisition

Cognizant Programmer: Ron Alley		12/10/85

.LEVEL1
.VARIABLE INP
Input dataset name
.VARIABLE OUT
Output dataset name(s)
.VARIABLE CHAN
Channels to be logged
.VARIABLE DATE
Date of data acquisition
ddmmmyy
.VARIABLE SITE
Site name
.VARIABLE FLIGHT
NASA/AMES flight number
.VARIABLE LINE
Line number for this flight
.VARIABLE RUN
Run number for this flight line
.VARIABLE TEXT
Channel descriptions to be
entered into the VICAR label
.LEVEL2
.VARIABLE INP
The name of the input dataset
.VARIABLE OUT
The name(s) of the output dataset(s).
.VARIABLE CHAN
Channels to be logged. The default is to log all channels, in order. You
may select any subset to be logged; they will be output IN THE ORDER LISTED.
.VARIABLE DATE
Date of data acquisition. This is the date that the mission was flown. The
date is usually written on the tape label, or it can be retrieved from the
flight log. The preferred format is ddmmmyy, e.g. 01FEB85.
.VARIABLE SITE
This is the user specified site name.
.VARIABLE FLIGHT
NASA/AMES flight number. This is usually written on the tape label; otherwise
it can be obtained from the flight log.
.VARIABLE LINE
Line number. This is usually written on the external tape label.
.VARIABLE RUN
Run number for this flight line. This is usually written on the external
label of the tape.
.VARIABLE TEXT
Channel descriptions to be entered into the VICAR label. If you wish to add
annotation to individual channels, you may do so by entering the default string
followed by the desired additional information. You are urged to always include
the default string, unless the text is incorrect for this data.
