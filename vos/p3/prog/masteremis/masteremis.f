C
C	This program computes the temperature and spectral emissivity for
C	the pixels of a MASTER TIR radiance image.
C
C	July 13, 2001      ...rea... Initial release
C	September 25, 2002 ...rea... Add 8CHANFIT mode
C	April 8, 2003      ...rea... Fix bug for REFCHAN=50 case
C	November 10, 2004  ...rea... Change to unscaled real values for both
C				     input and output.
C
	INCLUDE 'VICMAIN_FOR'
	SUBROUTINE MAIN44
C
	IMPLICIT NONE
	REAL X,EMIS,BUF(5000)/5000*0.0/
	REAL SKYRAD(10)/10*0.0/
	REAL PI/3.141593/
	INTEGER IN,IOUT,IOUT2,ISTAT,ISL,ISS,NL,NS,NLIN,NSIN,NUM,IDEF
	INTEGER IREF,IFIT,I,J,LUTUNIT1,LUTUNIT2
	INTEGER*2 RAD_LUT(40000,10),TEMP_LUT(32767,10)
	LOGICAL XVPTST,QBBFIT,QFIT8
	CHARACTER*100 LABEL
	CHARACTER*7 ORDINAL(10)/'       ',' second',' third ',' fourth',
     +	    ' fifth ',' sixth' ,'seventh',' eighth',' ninth ',' tenth '/
C
C						   Open datasets, get size field
	CALL XVUNIT(IN,'INP',1,ISTAT,' ')
	
	CALL XVOPEN(IN,ISTAT,'IO_ACT','SA','OPEN_ACT','SA',
     +		    'U_FORMAT','REAL',' ')
	CALL XVSIZE(ISL,ISS,NL,NS,NLIN,NSIN)
	CALL XVUNIT(IOUT2,'OUT',2,ISTAT,' ')
	CALL XVOPEN(IOUT2,ISTAT,'IO_ACT','SA','OPEN_ACT','SA',
     +		 'OP','WRITE','U_NL',NL,'U_NS',NS,'U_NB',1,
     +		 'U_ORG','BSQ','U_FORMAT','REAL','O_FORMAT','REAL',' ')
	CALL XVUNIT(IOUT,'OUT',1,ISTAT,' ')
	IF (XVPTST('BSQ')) THEN
	    CALL XVOPEN(IOUT,ISTAT,'IO_ACT','SA','OPEN_ACT','SA',
     +			'OP','WRITE','U_ORG','BSQ','U_NL',NL,'U_NS',NS,
     +			'U_FORMAT','REAL','O_FORMAT','REAL',' ')
	    DO I=1,10
		DO J=1,NL
		    CALL XVWRIT(IOUT,BUF,ISTAT,' ')
		END DO
	    END DO
	    CALL XVCLOSE(IOUT,ISTAT,' ')
	    CALL XVOPEN(IOUT,ISTAT,'IO_ACT','SA','OPEN_ACT','SA',
     +	           'OP','UPDATE','U_FORMAT','REAL',' ')
	ELSE
	    CALL XVOPEN(IOUT,ISTAT,'IO_ACT','SA','OPEN_ACT','SA',
     +	           'OP','WRITE','U_ORG','BIL','U_NL',NL,'U_NS',NS,
     +		   'U_FORMAT','REAL','O_FORMAT','REAL',' ')
	END IF
C							Get the other parameters
	QFIT8 = XVPTST('8CHANFIT')
	QBBFIT = XVPTST('BBFIT')
	CALL XVPARM('EMIS',EMIS,NUM,IDEF,0)
	CALL XVPARM('REFCHAN',IREF,NUM,IDEF,0)
	CALL XVPARM('FITCHAN',IFIT,NUM,IDEF,0)
C							       Get lookup tables
	CALL XVUNIT(LUTUNIT1,'INP',2,ISTAT,' ')
	CALL XVOPEN(LUTUNIT1,ISTAT,'IO_ACT','SA','OPEN_ACT','SA',' ')
	CALL XVUNIT(LUTUNIT2,'INP',3,ISTAT,' ')
	CALL XVOPEN(LUTUNIT2,ISTAT,'IO_ACT','SA','OPEN_ACT','SA',' ')
	DO I=1,10
	    CALL XVREAD(LUTUNIT1,RAD_LUT(1,I),ISTAT,' ')
	    CALL XVREAD(LUTUNIT2,TEMP_LUT(1,I),ISTAT,' ')
	END DO
	CALL XVCLOSE(LUTUNIT1,ISTAT,' ')
	CALL XVCLOSE(LUTUNIT2,ISTAT,' ')
C						     Get downwelling irradiances
	X = 0.0
	CALL XLGET(IN,'HISTORY','SKY41',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(1) = X / (1000.0*PI)
	CALL XLGET(IN,'HISTORY','SKY42',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(2) = X / (1000.0*PI)
	CALL XLGET(IN,'HISTORY','SKY43',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(3) = X / (1000.0*PI)
	CALL XLGET(IN,'HISTORY','SKY44',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(4) = X / (1000.0*PI)
	CALL XLGET(IN,'HISTORY','SKY45',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(5) = X / (1000.0*PI)
	CALL XLGET(IN,'HISTORY','SKY46',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(6) = X / (1000.0*PI)
	CALL XLGET(IN,'HISTORY','SKY47',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(7) = X / (1000.0*PI)
	CALL XLGET(IN,'HISTORY','SKY48',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(8) = X / (1000.0*PI)
	CALL XLGET(IN,'HISTORY','SKY49',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(9) = X / (1000.0*PI)
	CALL XLGET(IN,'HISTORY','SKY50',X,ISTAT,'FORMAT','REAL',' ')
	SKYRAD(10) = X / (1000.0*PI)
C					    Remove obsolete VICAR history labels
	CALL XLDEL(IOUT,'HISTORY','LBL1',ISTAT,'HIST','MASTERTI',' ')
	CALL XLDEL(IOUT,'HISTORY','LBL2',ISTAT,'HIST','MASTERTI',' ')
	CALL XLDEL(IOUT,'HISTORY','LBL3',ISTAT,'HIST','MASTERTI',' ')
	CALL XLDEL(IOUT2,'HISTORY','LBL1',ISTAT,'HIST','MASTERTI',' ')
	CALL XLDEL(IOUT2,'HISTORY','LBL2',ISTAT,'HIST','MASTERTI',' ')
	CALL XLDEL(IOUT2,'HISTORY','LBL3',ISTAT,'HIST','MASTERTI',' ')
C						    Add new VICAR history labels
	CALL XLADD(IOUT,'HISTORY','LBL1','Emissivity Image',
     &			ISTAT,'FORMAT','STRING',' ')
	CALL XLADD(IOUT2,'HISTORY','LBL1','Ground Temperature Image',
     &			ISTAT,'FORMAT','STRING',' ')
	CALL XLADD(IOUT2,'HISTORY','LBL2',
     &	      'Units = Degrees Celsius',ISTAT,'FORMAT','STRING',' ')
C
	IF (QBBFIT) THEN
C									bbfit
	    WRITE (LABEL,100) EMIS,ORDINAL(IFIT)
  100	    FORMAT(F5.3,' Emittance graybody fit to ',A7,
     +		   ' highest spectral emittance')
	    CALL XLADD(IOUT,'HISTORY','METHOD',LABEL,ISTAT,
     &			'FORMAT','STRING',' ')
	    CALL XLADD(IOUT2,'HISTORY','METHOD',LABEL,ISTAT,
     &			'FORMAT','STRING',' ')
	    CALL EMISCAL2(IN,IOUT,IOUT2,ISL,ISS,NL,NS,IFIT,EMIS,SKYRAD,
     +			  RAD_LUT,TEMP_LUT,QFIT8)
	ELSE IF (QFIT8) THEN
	    WRITE (LABEL,150) EMIS,ORDINAL(IFIT)
  150	    FORMAT(F5.3,' Emittance graybody fit to ',A7,
     +		   ' highest spectral emittance of Bands 42-49')
	    CALL XLADD(IOUT,'HISTORY','METHOD',LABEL,ISTAT,
     &			'FORMAT','STRING',' ')
	    CALL XLADD(IOUT2,'HISTORY','METHOD',LABEL,ISTAT,
     &			'FORMAT','STRING',' ')
	    CALL EMISCAL3(IN,IOUT,IOUT2,ISL,ISS,NL,NS,IFIT,EMIS,SKYRAD,
     +			  RAD_LUT,TEMP_LUT,QFIT8)
	ELSE
C									refchan
     	    WRITE (LABEL,200) EMIS,IREF
  200	    FORMAT(F5.3,' emittance fit to reference Channel ',I2)
   	    CALL XLADD(IOUT,'HISTORY','METHOD',LABEL,ISTAT,
     &		       'FORMAT','STRING',' ')
  	    CALL XLADD(IOUT2,'HISTORY','METHOD',LABEL,ISTAT,
     &			'FORMAT','STRING',' ')
	    IREF = MOD(IREF,10)
	    IF (IREF .EQ. 0) IREF = 10
	    CALL EMISCAL(IN,IOUT,IOUT2,ISL,ISS,NL,NS,IREF,EMIS,SKYRAD,
     +			 RAD_LUT,TEMP_LUT)
	END IF
C								Close datasets
	CALL XVCLOSE(IN,ISTAT,' ')
	CALL XVCLOSE(IOUT,ISTAT,' ')
	CALL XVCLOSE(IOUT2,ISTAT,' ')
	RETURN
	END
C***********************************************************************
	SUBROUTINE EMISCAL(IN,IOUT,IOUT2,ISL,ISS,NL,NS,IREF,EMIS,
     +			   SKYRAD,RAD_LUT,TEMP_LUT)
C
C	This routine computes emissivities, using the reference channel
C	method.
C
	IMPLICIT NONE
	REAL EMIS,SKYRAD(10),BUFIN(5000),BBRAD(5000,10),OUTBUF(5000),X
	REAL FRAC,TEMP
	INTEGER IN,IOUT,IOUT2,ISL,ISS,NL,NS,IREF,ILINE,ISAMP,ISTAT,I,J
	INTEGER ICHAN,INDEX
	INTEGER*2 RAD_LUT(40000,10),TEMP_LUT(32767,10)
C
	ILINE = ISL
	DO I=1,NL
	    CALL XVREAD(IN,BUFIN,ISTAT,'LINE',ILINE,'BAND',IREF,' ')
	    ISAMP = ISS
	    DO J=1,NS
		X = 1000.0*(BUFIN(ISAMP)-(1.0-EMIS)*SKYRAD(IREF)) / EMIS
		INDEX = MIN(32766.0,MAX(1.0,X))
		FRAC = X - INDEX
		TEMP = ((1.0-FRAC)*TEMP_LUT(INDEX,IREF) +
     +			FRAC*TEMP_LUT(INDEX+1,IREF)) / 100.0
		OUTBUF(J) = TEMP
		INDEX = 100.0 * (TEMP + 273.15)
		INDEX = MIN(39999,MAX(1,INDEX))
		FRAC = (100.0 * (TEMP + 273.15)) - INDEX
		DO ICHAN=1,10
		    BBRAD(J,ICHAN) = ((1.0-FRAC)*RAD_LUT(INDEX,ICHAN) +
     +				   FRAC*RAD_LUT(INDEX+1,ICHAN)) / 1000.0
C                                               BBRAD(J,ICHAN) is the blackbody
C                                               radiance of channel ICHAN at
C                                               temperature TEMP.
		END DO
		ISAMP = ISAMP+1
	    END DO
	    CALL XVWRIT(IOUT2,OUTBUF,ISTAT,' ')
	    DO ICHAN=1,10
		CALL XVREAD(IN,BUFIN,ISTAT,'LINE',ILINE,
     +			    'BAND',ICHAN,' ')
		ISAMP = ISS
		DO J=1,NS
		    OUTBUF(J) = (BUFIN(ISAMP)-SKYRAD(ICHAN)) /
     +				(BBRAD(J,ICHAN)-SKYRAD(ICHAN))
		    ISAMP = ISAMP+1
		END DO
		CALL XVWRIT(IOUT,OUTBUF,ISTAT,'LINE',I,'BAND',ICHAN,' ')
	    END DO
	    ILINE = ILINE+1
	END DO
	RETURN
	END
C***********************************************************************
	SUBROUTINE EMISCAL2(IN,IOUT,IOUT2,ISL,ISS,NL,NS,IFIT,EMIS,
     +			    SKYRAD,RAD_LUT,TEMP_LUT)
C
C	This routine computes emissivities, using the blackbody (graybody)
C	fitting method.
C
	IMPLICIT NONE
	REAL SKYRAD(10),BUFIN(5000,10),OUTBUF(5000,10),TBUF(5000)
	REAL EMIS,X,TEMP,FRAC,TEMPX(10)
	INTEGER IN,IOUT,IOUT2,ISL,ISS,NL,NS,IEL,ILINE,ISAMP,ICHAN,ISTAT
	INTEGER IFIT,INDEX
	INTEGER*2 RAD_LUT(40000,10),TEMP_LUT(32767,10)
C
	IFIT = 11 - IFIT
C
	IEL = ISL + NL - 1
	DO ILINE=ISL,IEL
	    DO ICHAN=1,10
		CALL XVREAD(IN,BUFIN(1,ICHAN),ISTAT,'LINE',ILINE,
     +			    'SAMP',ISS,'NSAMPS',NS,'BAND',ICHAN,' ')
	    END DO
	    DO ISAMP=1,NS
C						Find the max temperature that is
C						consistent with the given max e
		DO ICHAN=1,10
		    X = 1000.0 * (BUFIN(ISAMP,ICHAN) -
     +				  (1.0-EMIS)*SKYRAD(ICHAN)) / EMIS
		    INDEX = MIN(32766.0,MAX(1.0,X))
		    FRAC = X - INDEX
		    TEMPX(ICHAN) = ((1.0-FRAC)*TEMP_LUT(INDEX,ICHAN) +
     +				   FRAC*TEMP_LUT(INDEX+1,ICHAN)) / 100.0
		END DO
		CALL SORTR(TEMPX,10)
		TEMP = TEMPX(IFIT)
		TBUF(ISAMP) = TEMP
		INDEX = 100.0 * (TEMP + 273.15)
		INDEX = MIN(39999,MAX(1,INDEX))
		DO ICHAN=1,10
		    OUTBUF(ISAMP,ICHAN) = 
     +			(BUFIN(ISAMP,ICHAN)-SKYRAD(ICHAN)) /
     +			((RAD_LUT(INDEX,ICHAN)/1000.0)-SKYRAD(ICHAN))
		END DO
	    END DO
	    CALL XVWRIT(IOUT2,TBUF,ISTAT,' ')
	    DO ICHAN=1,10
		CALL XVWRIT(IOUT,OUTBUF(1,ICHAN),ISTAT,
     +			    'LINE',ILINE-ISL+1,'BAND',ICHAN,' ')
	    END DO
	END DO
	RETURN
	END
C***********************************************************************
	SUBROUTINE EMISCAL3(IN,IOUT,IOUT2,ISL,ISS,NL,NS,IFIT,EMIS,
     +			    SKYRAD,RAD_LUT,TEMP_LUT)
C
C	This routine computes emissivities, using the blackbody (graybody)
C	fitting method, but excludes Bands 41 and 50 from the list of
C	bands used for the fit.
C
	IMPLICIT NONE
	REAL SKYRAD(10),BUFIN(5000,10),OUTBUF(5000,10),TBUF(5000)
	REAL EMIS,X,TEMP,FRAC,TEMPX(10)
	INTEGER IN,IOUT,IOUT2,ISL,ISS,NL,NS,IEL,ILINE,ISAMP,ICHAN,ISTAT
	INTEGER IFIT,INDEX
	INTEGER*2 RAD_LUT(40000,10),TEMP_LUT(32767,10)
C
	IFIT = 9 - IFIT
C
	IEL = ISL + NL - 1
	DO ILINE=ISL,IEL
	    DO ICHAN=1,10
		CALL XVREAD(IN,BUFIN(1,ICHAN),ISTAT,'LINE',ILINE,
     +			    'SAMP',ISS,'NSAMPS',NS,'BAND',ICHAN,' ')
	    END DO
	    DO ISAMP=1,NS
C						Find the max temperature that is
C						consistent with the given max e
		DO ICHAN=2,9
		    X = 1000.0 * (BUFIN(ISAMP,ICHAN) -
     +				  (1.0-EMIS)*SKYRAD(ICHAN)) / EMIS
		    INDEX = MIN(32766.0,MAX(1.0,X))
		    FRAC = X - INDEX
		    TEMPX(ICHAN-1) = ((1.0-FRAC)*TEMP_LUT(INDEX,ICHAN) +
     +				   FRAC*TEMP_LUT(INDEX+1,ICHAN)) / 100.0
		END DO
		CALL SORTR(TEMPX,8)
		TEMP = TEMPX(IFIT)
		TBUF(ISAMP) = TEMP
		INDEX = 100.0 * (TEMP + 273.15)
		INDEX = MIN(39999,MAX(1,INDEX))
		DO ICHAN=1,10
		    OUTBUF(ISAMP,ICHAN) =
     +			(BUFIN(ISAMP,ICHAN)-SKYRAD(ICHAN)) /
     +			((RAD_LUT(INDEX,ICHAN)/1000.0)-SKYRAD(ICHAN))
		END DO
	    END DO
	    CALL XVWRIT(IOUT2,TBUF,ISTAT,' ')
	    DO ICHAN=1,10
		CALL XVWRIT(IOUT,OUTBUF(1,ICHAN),ISTAT,
     +			    'LINE',ILINE-ISL+1,'BAND',ICHAN,' ')
	    END DO
	END DO
	RETURN
	END
