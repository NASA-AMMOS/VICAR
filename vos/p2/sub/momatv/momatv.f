c
c/*      Oct 92   ...WPL...   Convert to UNIX */
C/*   10 AUG 83   ...CCA...   CONVERT TO VAX */
C/*   11 JAN 78   ...JJL...   INITIAL RELEASE */
c
      SUBROUTINE MOMATV(L0,S0,LSSP,SSSP,PIXPMM,FOC,BL,PHI,THT,VABS,
     #MATRIX,VECTOR)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 VECTOR(3)
      REAL*8 MATRIX(3,3),LL,L0,LSSP
c     LOGICAL*1 IO(36)/' ','I','N','P','U','T',' ','T','O',' ','M','0',
c    &'M','A','T','-','-','S','S','C','P','T','=','(',' ',' ',' ',' ',
c    &' ',',',' ',' ',' ',' ',' ',')'/
      Character*80 IO
      Data  IO /'INPUT TO MOMAT--SSCPT=(     ,     )'/
C
C   ********************************************************************
C   *                                                                  *
C   * THIS ROUTINE COMPUTES THE PLANET TO CAMERA ROTATION MATRIX AND   *
C   *    THE VECTOR FROM PLANET CENTER TO SPACECRAFT, EXPRESSED IN THE *
C   *    PLANET SYSTEM.  A VECTOR IN THE CAMERA SYSTEM, VPR, IS THEN   *
C   *    GIVEN IN TERMS OF THE SAME VECTOR IN THE PLANET SYSTEM, V, BY *
C   *                  VPR = (MATRIX)*(V-VECTOR)                       *
C   *                                                                  *
C   *                           INPUT VARIABLES                        *
C   *   NAME             TYPE                MEANING                   *
C   *                                                                  *
C   *    L0               R*8   LINE COORDINATE OF CAMERA AXIS         *
C   *    S0               R*8   SAMPLE COORDINATE OF CAMERA AXIS       *
C   *   LSSP              R*8   LINE COORDINATE OF SUBSPACECRAFT POINT *
C   *   SSSP              R*8   SAMPLE COORD.   OF SUBSPACECRAFT POINT *
C   *       (ALL OF THE ABOVE FOR DISTORTION-CORRECTED PICTURES)       *
C   *   PIXPMM            R*8   NUMBER OF PIXELS PER MM                *
C   *   FOC               R*8   CAMERA FOCAL LENGTH, MM                *
C   *    BL               R*8   WEST LONGITUDE OF SSP--DEGREES         *
C   *   PHI               R*8   LATITUDE OF SSP--DEGREES               *
C   *   THT (SEE SPECIAL  R*8   ANGLE OF PLANETARY SPIN AXIS (NORTH),  *
C   *        CASE LIST)         MEASURED IN PICTURE PLANE AT SSP,      *
C   *                           CLOCKWISE FROM UP--DEGREES             *
C   *   VABS              R*8   DISTANCE TO PLANET FROM S/C--KM        *
C   *                                                                  *
C   *                    OUTPUT VARIABLES                              *
C   *   MATRIX(3,3)       R*8   ROTATION MATRIX FROM PLANET TO CAMERA  *
C   *   VECTOR(3)         R*8   TRANSLATION VECTOR, IN PLANET SYSTEM
C   *                                                                  *
C   * THE FOLLOWING SPECIAL CASES CAN BE HANDLED BY THE PROGRAM IF THE *
C   * INPUT VARIABLES ARE PROPERLY SET                                 *
C   *               CASE 1 THE SSP IS A POLE, NOT IN THE PICTURE CENTER*
C   *          PROCEDURE   BL IS MEANINGLESS AND NOT LOOKED AT.  THT AS*
C   *                      DEFINED ABOVE IS ALSO MEANINGLESS.  IT WILL *
C   *                      BE ASSUMED TO CONTAIN A POSITIVE NUMBER     *
C   *                      WHICH EQUALS THE WEST LOMGITUDE OF THE      *
C   *                      PICTURE CENTER.                             *
C   *                                                                  *
C   *               CASE 2 THE SSP IS BOTH A POLE AND THE PICTURE      *
C   *                      CENTER                                      *
C   *          PROCEDURE   THE ROTATION IS TRIVIAL, BUT BL & THT ARE   *
C   *                      AGAIN MEANINGLESS, SO ADDITIONAL INFORMATION*
C   *                      IS REQUIRED.  SET THT (NORANG) TO A POSITIVE*
C   *                      NUMBER EQUALLING THE W LONGITUDE OF THE UP- *
C   *                      GOING MERIDIAN IN THE INPUT PICTURE.        *
C   *                                                                  *
C   *                                                                  *
C   * NOTE--CASES SUCH AS THE SSP BEING IN PICTURE CENTER BUT NOT A    *
C   *      POLE, OR POLE BEING IN PICTURE CENTER BUT NOT THE SSP, DO   *
C   *       NOT NEED SPECIAL HANDLING.                                 *
C   * NOTE--IN ALL CASES, VARIABLES OTHER THAN THT AND BL MUST BE      *
C   *       PROPERLY SET.                                              *
C   *                                                                  *
C   ********************************************************************
C
C
c     CALL OUTCON(SNGL(LSSP),IO(29),5,0)
c
      II = NINT(LSSP)
      JJ = NINT(SSSP)
      Write (IO(24:28),  '(I5)')  II
      Write (IO(30:34),  '(I5)') JJ
c     CALL OUTCON(SNGL(SSSP),IO(35),5,0)
c     CALL QPRINT(IO,36)
      Call Xvmessage(IO, ' ')
c
C     INITIALIZE, AND GET ALL ANGLES IN RADIANS
C
      PI=4.D0*DATAN(1.D0)
      DEGRAD=PI/180.D0
      BL=BL*DEGRAD
      PHI=PHI*DEGRAD
      THT=THT*DEGRAD
C
C     HERE COMPUTE LL, ANGLE BETWEEN ORIGINAL X-AXIS AND PROJECTION OF S
C     IN IMAGE PLANE, AND R, ANGLE AT CAMERA OBJECTIVE BETWEEN SSP AND O
C     AXIS.  BRANCH TO 100 IF OPTICAL AXIS POINTS TO SSP
C
      CONRAD = 1.D0/FOC/PIXPMM
      QLDIF=LSSP-L0
      QSDIF=SSSP-S0
      IF(QSDIF.EQ.0.D0) GO TO 13
      LL=DATAN2(QLDIF,QSDIF)
      GO TO 14
13    IF(QLDIF.EQ.0.D0) GO TO 100
      LL=DSIGN(PI/2.D0,QLDIF)
      GO TO 14
100   LL=0.D0
14    R=CONRAD*DSQRT(QLDIF*QLDIF+QSDIF*QSDIF)
      IF(DABS(PHI).EQ.PI/2.D0.AND.R.EQ.0.D0) GO TO 107
C
C     COMPUTE SINES AND COSINES OF ALL ANGLES, TEST FOR CASE 3
C
      SINLL=DSIN(LL)
      COSLL=DCOS(LL)
      SINR=DSIN(R)
      COSR=DCOS(R)
      IF(DABS(PHI).EQ.PI/2.D0) GO TO 101
      SINPHI=DSIN(PHI)
      COSPHI=DCOS(PHI)
      SINBL=DSIN(BL)
      COSBL=DCOS(BL)
C
C     IF PROGRAM GETS HERE, THE SSP IS NOT A POLE.  IN SUCH A CASE FIVE
C     SEPARATE ROTATIONS FROM CAMERA TO PLANET SYSTEM ARE REQUIRED.  THE
C
C     #1) +LL ABOUT Z-AXIS (+ MEANS COUNTERCLOCKWISE VIEWED FROM POSITIV
C                                END OF AXIS OF ROTATION)
C     #2) +R ABOUT Y-AXIS
C     #3) -PSI ABOUT Z-AXIS
C     #4) 90 + PHI ABOUT X-AXIS
C     #5) 90 + BL ABOUT Z-AXIS
C
C     ALL ANGLES HAVE BEEN PREVIOUSLY EXPLAINED EXCEPT PSI.  PSI IS THE
C     ANGLE BETWEEN UP AND NORTH MEASURED AT THE SSP IN THE IMAGE PLANE
C     AS IT STANDS AFTER THE SECOND ROTATION.  IF IT WEREN'T FOR R2,
C     PSI WOULD = (THT-LL), BUT NOW THAT RELATION IS ONLY APPROXIMATE,
C     WITH THE ERROR THE ORDER OF R.  THE NEXT ORDER OF BUSINESS IS TO
C     COMPUTE PSI.
C
      IF(R.EQ.0.D0) GO TO 400
      IF(DMOD(LL-THT+2.D0*PI,2.D0*PI).EQ.1.5D0*PI) GO TO 200
      DEL=DASIN(-SINR*DCOS(THT-LL)/COSPHI)
      A=DSIN((R+PI/2.D0-PHI)/2.D0)
      B=DSIN((R-PI/2.D0+PHI)/2.D0)
      C=DTAN((PI/2.D0+DEL-THT+LL)/2.D0)
      IF(A*C.EQ.0.D0) GO TO 1
      PSI=PI/2.D0+DATAN(B/A/C)*2.D0
      GO TO 2
C     NP VECTOR ANTI-PARALLEL TO CENTER-SSP LINE
1     PSI=PI/2.D0
      GO TO 2
C     NP VECTOR PARALLEL TO CENTER-SSP LINE
200   PSI=1.5D0*PI
      GO TO 2
C     SSP IN PICTURE CENTER
400   PSI=-THT
2     SINPSI=DSIN(PSI)
      COSPSI=DCOS(PSI)
C
C     FILL THE OMMATRIX.  CONVENTIONS: (I,J) = I ROW, J COLUMN.  GIVEN M
C     MULTIPLIES FROM LEFT ON COLUMN VECTORS, GOES FROM PLANET TO CAMERA
C     SYSTEMS.
C
      MATRIX(1,1)=+COSLL*COSR*COSPSI*SINBL+COSLL*COSR*SINPSI*SINPHI*COSB
     #L-COSLL*SINR*COSPHI*COSBL+SINLL*SINPSI*SINBL-SINLL*COSPSI*SINPHI*
     #COSBL
C
      MATRIX(2,1)=+SINLL*COSR*COSPSI*SINBL+SINLL*COSR*SINPSI*SINPHI*COSB
     #L-SINLL*SINR*COSPHI*COSBL-COSLL*SINPSI*SINBL+COSLL*COSPSI*SINPHI*
     #COSBL
C
      MATRIX(3,1)=-SINR*COSPSI*SINBL-SINR*SINPSI*SINPHI*COSBL-COSR*COSPH
     #I*COSBL
C
      MATRIX(1,2)=+COSLL*COSR*COSPSI*COSBL-COSLL*COSR*SINPSI*SINPHI*SINB
     #L+COSLL*SINR*COSPHI*SINBL+SINLL*SINPSI*COSBL+SINLL*COSPSI*SINPHI*
     #SINBL
C
      MATRIX(2,2)=+SINLL*COSR*COSPSI*COSBL-SINLL*COSR*SINPSI*SINPHI*SINB
     #L+SINLL*SINR*COSPHI*SINBL-COSLL*SINPSI*COSBL-COSLL*COSPSI*SINPHI*
     #SINBL
C
      MATRIX(3,2)=-SINR*COSPSI*COSBL+SINR*SINPSI*SINPHI*SINBL+COSR*COSPH
     #I*SINBL
C
      MATRIX(1,3)=-COSLL*COSR*SINPSI*COSPHI-COSLL*SINR*SINPHI+SINLL*COSP
     #SI*COSPHI
C
      MATRIX(2,3)=-SINLL*COSR*SINPSI*COSPHI-SINLL*SINR*SINPHI-COSLL*COSP
     #SI*COSPHI
C
      MATRIX(3,3)=+SINR*SINPSI*COSPHI-COSR*SINPHI
C
C     COMPUTE VECTOR FROM PLANET CENTER TO S/C, IN CAMERA SYSTEM
C
3     VECTOR(1)=-VABS*COSLL*SINR
      VECTOR(2)=-VABS*SINLL*SINR
      VECTOR(3)=-VABS*COSR
C
C     ROTATE VECTOR TO PLANET SYSTEM
C
40    A=VECTOR(1)
      B=VECTOR(2)
      C=VECTOR(3)
      VECTOR(1)=MATRIX(1,1)*A+MATRIX(2,1)*B+MATRIX(3,1)*C
      VECTOR(2)=MATRIX(1,2)*A+MATRIX(2,2)*B+MATRIX(3,2)*C
      VECTOR(3)=MATRIX(1,3)*A+MATRIX(2,3)*B+MATRIX(3,3)*C
      RETURN
C
C     COME HERE IF SSP IS A PLANET POLE NOT AT PICTURE CENTER.
C
101   SINTHT=DSIN(THT)
      COSTHT=DCOS(THT)
      MATRIX(1,1)=-COSLL*COSR*COSTHT-SINLL*SINTHT
      MATRIX(1,2)=+COSLL*COSR*SINTHT-SINLL*COSTHT
      MATRIX(1,3)=-COSLL*SINR
      MATRIX(2,1)=-SINLL*COSR*COSTHT+COSLL*SINTHT
      MATRIX(2,2)=+SINLL*COSR*SINTHT+COSLL*COSTHT
      MATRIX(2,3)=-SINLL*SINR
      MATRIX(3,1)=+SINR*COSTHT
      MATRIX(3,2)=-SINR*SINTHT
      MATRIX(3,3)=-COSR
      IF(PHI.GT.0.D0) GO TO 3
C
C     IF SSP WAS SOUTH POLE, MATRIX NEEDS REVISION
C
      DO 4 I=1,3
      DO 5 J=2,3
5     MATRIX(I,J)=-MATRIX(I,J)
4     CONTINUE
      GO TO 3
C
C     COME HERE IF SSP IS BOTH A POLE AND PICTURE CENTER
C
107   SINTHT=DSIN(THT)
      COSTHT=DCOS(THT)
      MATRIX(1,1)=SINTHT
      MATRIX(1,2)=-COSTHT
      MATRIX(1,3)=0.D0
      MATRIX(2,1)=COSTHT
      MATRIX(2,2)=SINTHT
      MATRIX(2,3)=0.D0
      MATRIX(3,1)=0.D0
      MATRIX(3,2)=0.D0
      MATRIX(3,3)=1.D0
      VECTOR(1)=0.
      VECTOR(2)=0.
      VECTOR(3)=-VABS
      IF(PHI.LT.0.D0) GO TO 40
      MATRIX(1,1)=-MATRIX(1,1)
      MATRIX(2,1)=-MATRIX(2,1)
      MATRIX(3,3)=-1.D0
      GO TO 40
      END
