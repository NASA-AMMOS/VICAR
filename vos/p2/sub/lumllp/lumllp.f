      SUBROUTINE LUMLLP(SLAT,SLON,PLAT,PLON,RPOL,REQU,RANGE,RLAT,RLON,
     *  TG,CG,CI,CE,LUMLAT,LUMLON,MODE,ifirst)

C  SLAT,SLON= SUBSOLAR POINT , WEST
C  PLAT,PLON= SUBSPACECRAFT POINT , WEST
C  RLAT,RLON = PIXEL POSITION , WEST
C  RPOL,REQU = PLANET RADIUS (KM)
C  ALL VALUES IN RADIANS
C  RANGE = S.C. TO PLANET CENTER DISTANSE (KM)   IF(RANGE)=0. MEANS=INFI
C  RETURNS TG=TAN PHASE ANGLE ,MAX VALUE LIMITED TO 1000.
C  RETURNS CG=COSINE PHASE ANGLE
C  RETURNS CI=COSINE INCIDENCE ANGLE
C  RETURNS CE=COSINE EMISSION ANGLE
C  IF RANGE=0.0 CE IS COMPUTED FOR RANGE=INFINITY
C  RETURNS LUMLAT,LUMLON = LUMINANCE LATITUDE,LONGITUDE  IF(MODE.NE.0)
C  LUMLON IS EAST LONGITUDE
C  MODE =0 FOR RETURN WITHOUT LUMLAT,LUMLON COMPUTED

      SAVE  ! SAVE VARIABLES BETWEEN SUCCESSIVE CALLS

      REAL AS(3),AL(3),C(10),HR(3,3),PI,PI2
      REAL LUMLAT,LUMLON,PIO2
      INTEGER FIRST
      DATA PI/3.141592654/,PI2/6.283185308/
      DATA PIO2/1.570796327/
      DATA FIRST/1/
C==================================================================
      IF (IFIRST.EQ.1) FIRST=1
      IF(FIRST.NE.1) GO TO 10

C  SET UP INITIALIZATION FOR FIRST PASS ONLY
      FIRST=0
      C(1)=COS(SLAT)
      C(2)=COS(PI2-SLON)
      C(3)=SIN(SLAT)
      C(4)=SIN(PI2-SLON)
      C(5)=COS(PLAT)
      C(6)=COS(PI2-PLON)
      C(7)=SIN(PLAT)
      C(8)=SIN(PI2-PLON)
      C(9)=C(5)*C(6)*C(1)*C(2)+C(5)*C(8)*C(1)*C(4)+C(7)*C(3)
      U=ACOS(C(9))
      IF(AMOD(SLON-PLON +4.*PI,PI2).GT.PI) U=-U
      C(10)=SIN(U)
      X=C(1)*C(4)*C(7)-C(5)*C(8)*C(3)
      Y=C(5)*C(6)*C(3)-C(1)*C(2)*C(7)
      Z=C(1)*C(2)*C(5)*C(8)-C(5)*C(6)*C(1)*C(4)
      AAA=0.0
      IF(X.NE.0.0.OR.Y.NE.0.0) AAA=ATAN2(Y,X)
      BBB=ASIN(Z/SQRT(X*X+Y*Y+Z*Z))
      CCC=PI2-PLON
      COSB=COS(BBB)
      SINB=SIN(BBB)
      COSA=COS(AAA)
      SINA=SIN(AAA)
      SINCA=SIN(CCC-AAA)
      COSCA=COS(CCC-AAA)
      IF(COSCA.EQ.0.) GO TO 411
      TANPSI=SINB*SINCA/COSCA
      COSPSI=-COSB*C(7)+SINB*C(5)*COSCA
      SINPSI=TANPSI*COSPSI
      GO TO 412
411   IF(SINB.EQ.0.) GO TO 413
      COSPSI=0.0
      SINPSI=SIGN(1.,SINB)
      GO TO 412
413   COSPSI=0.0
      SINPSI=1.0
      IF(AAA.NE.0.) SINPSI=-1.
412   HR(1,1)=COSPSI*SINB*COSA-SINPSI*SINA
      HR(1,2)=COSPSI*SINB*SINA+SINPSI*COSA
      HR(1,3)=-COSPSI*COSB
      HR(2,1)=-SINPSI*SINB*COSA-COSPSI*SINA
      HR(2,2)=-SINPSI*SINB*SINA+COSPSI*COSA
      HR(2,3)=SINPSI*COSB
      HR(3,1)=COSB*COSA
      HR(3,2)=COSB*SINA
      HR(3,3)=SINB
      RP=RPOL*RPOL
      RE=REQU*REQU
      RAD=RPOL
10    CONTINUE
      CA=COS(RLAT)
      CO=COS(PI2-RLON)
      SA=SIN(RLAT)
      SO=SIN(PI2-RLON)
      IF(RPOL.EQ.REQU) GO TO 11
      RAD=RE/SQRT(RE*CA*CA+RP*SA*SA)
11    CI=CA*CO*C(1)*C(2)+CA*SO*C(1)*C(4)+SA*C(3)
      CT=CA*CO*C(5)*C(6)+CA*SO*C(5)*C(8)+SA*C(7)
      CE=CT
      IF(RANGE.NE.0.) GO TO 20
      CG=C(9)
      GO TO 50
20    IF (CT.GT.1.0) CT=1.0
      IF (CT.LT.-1.0) CT=-1.0
      TH=ACOS(CT)
      ST=SIN(TH)
      IF(AMOD(CCC-(PI2-RLON)+4.*PI,PI2).GT.PI) ST=-ST
      DENOM=CT-RAD/RANGE
      IF(DENOM.NE.0.) GO TO 30
      CE=0.
      SE=SIGN(1.,ST)
      GO TO 40
30    E=ATAN2(ST,DENOM)
      CE=COS(E)
      SE=SIN(E)
40    if(st.eq.0.0)st=0.00001 ! try to avoid divide by zero
      CG=CI*CE+SE*(C(9)-CI*CT)/ST
50    TG=1000.
      G=ACOS(CG)
C  G LIES BETWEEN 0 AND PI
      IF(ABS(G-PIO2).GT..001) TG=TAN(G)
      IF(MODE.EQ.0) RETURN
      AS(1)=RAD*CO*CA
      AS(2)=RAD*SO*CA
      AS(3)=RAD*SA
      DO 70 I=1,3
          AL(I)=0.
          DO 71 II=1,3
71           AL(I)=AL(I)+AS(II)*HR(II,I)
70    CONTINUE
      LUMLON=ATAN2(AL(2),AL(1))*57.29578
      LUMLAT= ASIN(AL(3)/RAD)*57.29578
      RETURN
      END
