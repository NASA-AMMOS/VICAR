PROCEDURE help=*

! PROCEDURE TO PREPARE DATA FOR MOMGEN
! Will create Light Transfer Files from sets of
! images which have fixed radiances and increasing
! exposures or Reciprocity Files from sets of
! images where the radiances and exposures both vary

PARM LIST       TYPE=STRING
PARM LTFRCP     TYPE=STRING
PARM LTFILE     TYPE=STRING   COUNT=(0:1) DEFAULT=--

refgbl $BECHO
refgbl $syschar

BODY
LOCAL (F,G,LUMS,EXS)      STRING
LOCAL (NLVL,I1,I2,I3,I,J) INTEGER
LOCAL (EXP,LM,X)          REAL
LOCAL EX                  REAL      COUNT=1:100
LOCAL LMS                 STRING    COUNT=1:100

if ($syschar(1)="UNIX")
   defcmd-replace typeit "ush cat"
else
   defcmd-replace typeit "dcl type"
end-if
 
reset &LIST
 
!Read labels for exposure and radiance of first file in list
nxt &LIST F I1 I2 I3

getlab &F lab_item="EXPOSURE_DURATION" itm_name=EXP 'PROPERTY +
       itm_type=REAL itm_task="CASSINI-ISS"

let EX(1)=EXP
 
getlab &F lab_item="RADIANCE" itm_name=LM 'PROPERTY +
       itm_type=REAL itm_task="CASSINI-ISS"

let LUMS = "&LM"

let LMS(1)=LUMS
 
!Create and initialize the file to contain the filenames for the
!first exposure level.
createfile list1.dat
addtofile list1.dat "NEXT FILE=0001"
addtofile list1.dat "&F"

!Loop thru the files in the input list (which is in order of increasing
!exposure) and group into exposure groups (levels).  For each group, make
!a list of the filenames for submission to MOMGEN

let J=1                    !NUMBER OF EXPOSURE LEVELS
loop
   nxt &LIST F I1 I2 I3
   if (F="END_OF_FILE") break
   getlab &F lab_item="EXPOSURE_DURATION" itm_name=EXP 'PROPERTY +
          itm_type=REAL itm_task="CASSINI-ISS"
   getlab &F lab_item="RADIANCE" itm_name=LM 'PROPERTY +
          itm_type=REAL itm_task="CASSINI-ISS"
   let LUMS = "&LM"   !convert real to string 
   if (EXP=EX(J))
       let G="list"//"&J"//".dat"
       addtofile &G "&F"
   else
       let J=J+1
       let EX(J)=EXP
       let LMS(J) = LUMS
       let G="list"//"&J"//".dat"
       createfile &G
       addtofile &G "NEXT FILE=0001"
       addtofile &G "&F"
   end-if
end-loop
 
let NLVL=J             !NUMBER OF UNIQUE EXPOSURE TIMES
 
let $BECHO="NO"
write " NUMBER OF EXPOSURE LEVELS = &NLVL"
write " "
 
!print out all exposure levels found
write " "
write " EXPOSURE LEVEL ... EXPOSURE TIME ... RADIANCES"
let I=1
loop
   let X=EX(I)
   let EXS = "&X"
   let LUMS=LMS(I)
   write "       &I              &EXS             &LUMS"
   if (I=NLVL) break
   let I=I+1
end-loop
 
write " "
let $BECHO="YES"
 
!run MOMGEN for all exposure levels
let I=1
loop
   let X=EX(I)
   let EXS = "&X"   !convert real value to string 
   let G="list"//"&I"//".dat"
 
   let $BECHO="NO"
   write "FRAME LIST FOR LEVEL &I    EXPOSURE TIME &EXS"
   typeit &G
   write " "
   let $BECHO="YES"
 
   momgen LIST="list"&I".dat" out=&LTFRCP

 
   if (I=NLVL) break
   let I=I+1
end-loop
 
if ($count(LTFILE) = 1)
! Create and fill text file of radiance values for CCCDRECIP
! CCDRECIP expects radiance of dc to be 0.0
   createfile &LTFILE
   let I=1
   loop 
      let LUMS=LMS(I)
      if (EX(I)=0.0) let LUMS="0.0"
      addtofile &LTFILE "&LUMS"
      if (I=NLVL) break
      let I=I+1
   end-loop
end-if
 
end-proc
 
.TITLE
Vicar Procedure MOMGEN2
.HELP
PURPOSE:
 
Calls MOMGEN to fill Light Transfer Files or Reciprocity Files from sets of
images of known radiances and exposures.  These output files are
required for the programs CCDRECIP, CCDSLOPE and CCDNOISE.
 
EXECUTION:
 
        MOMGEN2 LIST=file LTFRCP=outfile LTFILE=ltfile
 
.PAGE
OPERATION:
 
MOMGEN2 processes frames of a Light-transfer or Reciprocity sequence
into a Light-transfer file or Reciprocity file.  All frames are input
in a SRCH-format list.  MOMGEN2 splits the list into multiple executions of
MOMGEN.
 
The input list of filenames is assumed to be in order of increasing
exposure time.  MOMGEN2 splits the frames into exposure groups by
reading the VICAR labels and calls MOMGEN to process each group.  The
Light-transfer file or Reciprocity file filled by multiple executions
of MOMGEN.
 
In addition, the radiances of the frames are read from the labels and,
if the LTFILE parameter is given, a text file listing the radiances
of each exposure group is written.  This file may be input to the
program CCDRECIP.
 
.PAGE
 
RESTRICTIONS:
 1.  The list of filenames must be in order of increasing exposure
     time.
 
 2.  The output Light-transfer or Reciprocity file must have been
     previously created and formatted by LTGEN.
 
.PAGE
ORIGINAL PROGRAMMER: Charlie Avis
COGNIZANT PROGRAMMER: Charlie Avis
REVISION HISTORY:

   20 Mar  97 ...T.Huang.... Ported from VAX to UNIX. 
   29 July 94 ...C.C.Avis... Initial release
 
.LEVEL1
.VARIABLE LIST
 A SRCH-format list
 of filenames to
 process.
 
.VARIABLE LTFRCP
 A Light-transfer file
 or Reciprocity file
 
.VARIABLE LTFILE
 A text file of
 radiance values
 
.LEVEL2
.VARIABLE LIST
 STRING
 Specifies the name of a SRCH-format file containing a list
 of filenames to process.  The files must be in order of increasing
 exposure time.  Multiple frames of the same exposure time are
 grouped for executions of MOMGEN.
 
.VARIABLE LTFRCP
 STRING
 Specifies the output Light-transfer file or Reciprocity file which
 is filled by the executions of MOMGEN.  It must have been previously
 created by LTGEN.  This file is necessary for input to programs
 CCDRECIP, CCDNOISE, and  CCDSLOPE.
 
.VARIABLE LTFILE
 STRING OPTIONAL
 Specifies an output file to contain a list of the radiance values
 for the unique exposure times.  Note:  If the exposure time = 0.0,
 the actual radiance value will be ignored and a value of 0.0 entered.
 This file may be input to CCDRECIP.
.END

