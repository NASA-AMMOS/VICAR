procedure
refgbl $echo
body
let _onfail="continue"
let $echo="yes"
!   TEST PDF FOR LGEOM
!
! generate BYTE input data set
! 
gen LGTEST 10 10 SINC=40 LINC=40
! 
! Verify existence of input file
list LGTEST
! 
! Perform simple enlargement to 2X size with NOIN and NOIZ
lgeom LGTEST LGENLARG NAH=1 NAV=1 SIZE=(1,1,20,20) +
  TIEPOIN=(1,1,1,1, 1,20,1,10, 20,1,10,1, 20,20,10,10) 'NOIN
! 
! Print it out
list LGENLARG
lgeom LGTEST LGENLARG NAH=1 NAV=1 SIZE=(1,1,20,20) +
  TIEPOIN=(1,1,1,1, 1,20,1,10, 20,1,10,1, 20,20,10,10) 'NOIZ
! 
! Print it out
list LGENLARG
! 
! Perform 90 degree rotation clockwise with two times enlargement
lgeom LGTEST LGROTAT SIZE=(1,1,20,20) NAH=1 NAV=1 +
 TIEPOIN=(1,1,10,1, 1,20,1,1, 20,1,10,10, 20,20,1,10)
!
! Print it out
list LGROTAT
! 
! Now do simple tests for halfword data
! 
! First generate input data set. (Try some negative DNs.)
gen LGTESTH 10 10 IVAL=-10000 LINC=3000 SINC=3000 'HALF
! 
! Verify its existance
list LGTESTH
! 
! Now do 2 times enlargement getting format from label
lgeom LGTESTH LGENLARGH SIZE=(1,1,20,20) NAH=1 NAV=1 +
 TIEPOIN=(1,1,1,1, 1,20,1,10, 20,1,10,1, 20,20,10,10) 
! 
! Print it out
list LGENLARGH
! 
! Now rotate 90 degrees
lgeom LGTESTH LGROTATH SIZE=(1,1,10,10) NAH=2 NAV=2 +
  TIEPOIN=(1,1,10,1,   1,6,5,1,   1,10,1,1, +
           6,1,10,6,   6,6,5,6,   6,10,1,6, +
	   10,1,10,10, 10,6,5,10, 10,10,1,10) 
! 
! Print it out
list LGROTATH
! 
! try again with SS not 1
lgeom LGTESTH LGROTATH SIZE=(1,3,10,8) NAH=2 NAV=2 +
  TIEPOIN=(1,1,10,1,   1,6,5,1,   1,10,1,1, +
           6,1,10,6,   6,6,5,6,   6,10,1,6, +
	   10,1,10,10, 10,6,5,10, 10,10,1,10) 
! 
! Print it out
list LGROTATH
! 
! Create a parameter data set 
tlgeom PDS
!
! Do halfword enlargement with parameter data set
lgeom (LGTESTH,PDS) LGENLARGH (1,1,20,20) 'PRINT
!
! Print it out
list LGENLARGH
! 
! now for a more complicated transformation, test 'PRINT
gen lgtest 512 512 sinc=0 linc=20
lgeom lgtest lgtout.img size=(1,1,512,512) nah=2 nav=2  'PRINT +
 tiepoin=(1,1,20,20, 1,256,100,200, 1,512,50,450, +
          256,1,180,120, 256,256,256,256, 256,512,150,350, +
	  512,1,380,50,  512,256,300,200, 512,512,512,512)
write "Display lgtout.img to demonstrate 4 twisted quadrants "
let $echo="no"
end-proc
