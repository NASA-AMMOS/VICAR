.TITLE	MVB
.PSECT	MVB
.ENTRY	MVB,^M<R2,R3,R4,R5,R6,R7,R8>
;	SRC	R6	Moves LEN bytes from SRC to DST.
;	DST	R7	SRC passed by reference.
;	LEN	R8	LEN and DST passed by reference.
;
	MOVL	4(AP),R6
	MOVL	8(AP),R7
	MOVL	@12(AP),R8
;
				; Here if source passed by reference
R6ADDR:	CMPL	R8,#^x0000FFFF	; See if more than one move is needed
	BLEQU	LSTMOV		; If not, goto the last move of the data
	MOVC3	#^xFFFF,(R6),(R7); Move the first chunk
	MOVL	R1,R6		; Increment the addresses to the next pos
	MOVL	R3,R7		; (MOVC3 sets R1 and R3 for us)
	SUBL2	#^x0000FFFF,R8	; Decrement the length to move
	BRB	R6ADDR		; Start the loop over

LSTMOV:	MOVC3	R8,(R6),(R7)
	RET
;
;	CALL	ZBA(LOC,NB)	ZERO NB BYTES STARTING AT LOC
	.ENTRY	ZBA,^M<R2,R3,R4,R5,R6,R7,R8,R9>
	CLRL	R6		;R6=0
	MOVL	4(AP),R7	;R7=LOC
	MOVL	@8(AP),R8	;R8=NB
	MOVL	#^XFFFF,R9	;R9=65535
10$:	CMPL	R8,R9
	BLEQU	20$		;BR IF LAST MOVE
	MOVC5	R6,(R7),R6,R9,(R7) ;MOVE 65535 BYTES
	MOVL	R3,R7		;UPDATE LOC
	SUBL2	R9,R8		;AND NB
	BRB	10$
;
20$:	MOVC5	R6,(R7),R6,R8,(R7) ;LAST MOVE
	RET
.END
