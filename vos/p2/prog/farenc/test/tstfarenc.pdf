! tstfarenc.pdf: Unit test for program FARENC
procedure
refgbl $autousage
refgbl $echo
refgbl $syschar
LOCAL DIR     TYPE=STRING 
LOCAL VGRIMG  TYPE=STRING 
LOCAL VENUS   TYPE=STRING
LOCAL PIX     TYPE=STRING
LOCAL GEO     TYPE=STRING
LOCAL GPR     TYPE=STRING
LOCAL RAW     TYPE=STRING
LOCAL SUMMATION   TYPE=STRING
body
let $autousage="none"
let _onfail="continue"
let $echo="yes"
!
!.........................Voyager test.........................................
if ($syschar(1) = "UNIX")
   LET DIR  ="/project/test_work/testdata/mipl/vgr/"
else 
   LET DIR  ="WMS_TEST_WORK:[TESTDATA.MIPL.VGR]"
end-if
LET RAW = "&DIR"//"f1636832.raw"	!Raw image (byte)
LET PIX = "&DIR"//"f1636832.fic"	!FICORed image (halfword)
LET GPR = "&DIR"//"f1636832.gpribis"	!Geometric correction parameters

!...Test on FICOR'd image.  SPICE data is not used.
!...Instead, all image geometry data are input via parameters.
farenc INP=(&PIX,&GPR) 'nosedr +
       requator=1830. rpole=1830. +
       rmag=806022 lati=-.0962 long=155. +
       nora=285 focal=1500.19 initcen=(600,600) +
       'auto nl=100 ns=100 resca=.8 area=(100 300 800 700)

!...The same as above except that SPICE data is used.  Results should match.
farenc INP=(&PIX,&GPR) target=IO +
      'auto nl=100 ns=100 resca=.8 area=(100 300 800 700)

!...Test on raw image.  Rescale image so that it fits in a 512x512 format with
!...target center at (256,256) with north pointing at 1 o'clock.
farenc INP=(&RAW,&GPR) OUT=(a.img,b.img,g.img) target=IO +
 'AUTO range=800. AREA=(100,200,600,500) +
  NL=512 NS=512 PUTNORTH=30. RESCALE=1. 
geoma INP=(&RAW,g.img) OUT=c.img NL=512 NS=512	!Project the image
list c.img LINC=35 SINC=35	!Low res printout of Io

!...Test on object-space image.  We do the same as before, but first projecting
!...the image to object space before running FARENC.  The outputs c.img and
!...d.img should be nearly identical, but a lot of things have to go right.
geoma INP=(&RAW,&GPR) OUT=os.img NL=1000 NS=1000
farenc INP=os.img  OUT=(a.img,b.img,g.img) target=IO +
 'AUTO range=800. AREA=(100,200,600,500) +
  NL=512 NS=512 PUTNORTH=30. RESCALE=1. 
geoma INP=(os.img,g.img) OUT=d.img NL=512 NS=512
list d.img LINC=35 SINC=35	!Low res printout of Io
f2 (c.img,d.img) diff.img func="in1-in2+128"
hist diff.img

!...Testing PERSLAB label interface.  PERSLAB will access the VGR SPICE kernels
!...and store that data into the label.  FARENC should not access SPICE
!...directly, but get the information from the label.
perslab os.img a.img target=IO
label-list a.img	!Print the PERSLAB label
farenc a.img
list FARENC.POS		!Print the target center output by FARENC
write "Should get  about 5.425E+02  6.07E+02"
!
!.........................Galileo test........................................
!
write " "
write " test of galileo"
write " "
if ($syschar(1) = "UNIX")
   LET DIR  ="/project/test_work/testdata/mipl/gll/"
else 
   LET DIR  ="WMS_TEST_WORK:[TESTDATA.MIPL.GLL]"
end-if
let VENUS= "&DIR"//"venus.img"
LET SUMMATION ="&DIR"//"s0349632122.1"         ! summation mode GALILEO image.
!
!...Test on full-frame image
farenc INP=&VENUS OUT=g.img begin=90 +
       radius=6138.2 'update 'auto below=500 dnth=500 'remote  
!...Use output GEOMA paramters (g.img) to recenter target at (400,400)
geoma INP=(&VENUS,g.img) OUT=f.img 
!...Run FARENC to check that center is at 400 400.
local (pl,ps) real
farenc INP=f.img begin=90 pcl=pl pcs=ps +
 radius=6138.2 'auto below=500 dnth=500 range=400. initcen=(399.,399.)
disp (pl,ps)
list  f.img LINC=80 SINC=80
!
!...Test on summation mode image
farenc &SUMMATION  'remote
!
!..........................Cassini test (only works on Unix).................
!
if ($syschar(1) = "UNIX")
  farenc INP=/project/cassini_work/spice_examples/n1354897340.1 +
    OUT=g.img 'auto initcen=(138,872) 
  geoma INP=(/project/cassini_work/spice_examples/n1354897340.1,g.img) OUT=f.img
end-if
!
!..........................Space Telescope test...............................
! TEST WITH PERSPECTIVE INPUT LABEL & SPACE TELESCOPE DATA
!perslab inp=(&INHLF, &INHDR, &INDAT) out=x.img north=93.
! the solution is about:  centline=246.79 centsamp=240.98
!farenc inp=x.img area=(60,60,680,680)
!list FARENC.POS
!
!........................test showing LSQP bug (fixed 20jul05).................
!... input is a band from a Cassini VIMS cube --  test is for unix only ...

! in old FARENC, this cmd gives infinite loop (on Solaris only) -- break it by doing
! 'kill -9' on taetm process from another window:
! after the fix, this command will result in an error message: "ERROR in routine LSQP"
farenc /project/test_work/testdata/mipl/cassini/farenc/ENC_S12_SQ10B.b166b (p,c) +
 solar=(-21.08 243.1) 'auto clus=(10,2)

! for completeness, show how to avoid error msg in fixed version by tweaking PERC
! parameter:
farenc /project/test_work/testdata/mipl/cassini/farenc/ENC_S12_SQ10B.b166b (p,c) +
 solar=(-21.08 243.1) 'auto clus=(10,2) perc=0.1

end-proc
