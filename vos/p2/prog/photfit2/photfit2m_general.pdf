procedure option=selftutor help=*

!-----------------------------------------------------------------------------
! PHOTFIT2M_GENERAL.PDF
!
! This is the PDF for the first menu point of the MDF file.
! In this PDF file general parameters are defined like the names of 
! input/output files.
!
!-----------------------------------------------------------------------------

	!**********************************************************************
	! The global variables PHO_FUNC_type (and $MENUS) will be used in this
	! procedure, so they have to be declared here.
	! The global PHO_FUNC_type (the desired photometric function) will be
	! used to change the third menu point of the MDF file (see above)
	! according to the input to PHO_FUNC in this PDF.
	! The global $MENUS keeps the active stack of Menu Definition File
	! names. $MENUS(1) is the root menu name, $MENUS(2) the menu selected
	! from the root menu, and so on. The current menu stack can be
	! displayed with "DISPLAY $MENUS". Since there is only one MDF in
	! this demo, it doesn't make too much sense to have that global
	! included here. It is just referenced for completeness.
	!**********************************************************************

	refgbl PHO_FUNC_type 
	refgbl $menus

	parm MAIN_PROC_NAME string

	procedure name=photfit2m_general_sub help=*

		! dummy inputs :

		parm inp	type=(string,72) count=(0:20) default=--

		! photometric functions :

		parm PHO_FUNC type=(string,32) count=1 	+
			valid = (			+
				LAMBERT,		+
				MINNAERT,		+
				IRVINE,			+
				VEVERKA,		+
				BURATTI1,		+
				BURATTI2,		+
				BURATTI3,		+
				MOSHER,			+
				LUMME_BOWEL_HG1,	+
				HAPKE_81_LE2,		+
				HAPKE_81_COOK,		+
				HAPKE_86_HG1,		+
				HAPKE_86_HG2,		+
				HAPKE_86_LE2,		+
				HAPKE_HG1_DOM,		+
				REGNER_HAPKE_HG1 	+
				) 	default="&PHO_FUNC_type"


		PARM CLASS_ID TYPE=INTEGER COUNT=0:1 valid=(0:32767) DEFAULT=--

  		PARM NORM    TYPE=INTEGER COUNT=1 VALID=(0:10000) DEFAULT=500
  		PARM RERUN   TYPE=INTEGER COUNT=0:1 DEFAULT=1
  		PARM MAXITER TYPE=INTEGER COUNT=0:1 DEFAULT=--
  		PARM NUMTEN  TYPE=INTEGER COUNT=0:1 DEFAULT=--
  		PARM PERCENT TYPE=REAL COUNT=1 VALID=(1.:100.) DEFAULT=90.
  		PARM TOLERANC TYPE=REAL COUNT=1 VALID=(0.:1.) DEFAULT=.02

!  		PARM PLOT     TYPE=KEYWORD DEFAULT=PLOT	+ 
!			      VALID=(PLOT, NOPLOT) 
  		PARM PRINT    TYPE=KEYWORD DEFAULT=PRINT +
			      VALID=(PRINT, NOPRINT) 
!  		PARM DISP TYPE=KEYWORD VALID=(DISP,NODISP,WAITNOMORE) +
!    				DEFAULT=NODISP
  		PARM METROP  TYPE=INTEGER COUNT=1 VALID=(0:10000) DEFAULT=0



	body

	!*******************************************************************
	! "pho_global.pdf" is being executed in the following line. 
	! In this PDF, two globals are defined (PHO_FUNC_type, pho_PROC_NAME)
	!*******************************************************************
 
	pho_global PHO_FUNC_type="&PHO_FUNC"

	end-proc
body

	if (_tutor=1)

	   restore-parm common_proc_name.par

 	   tutor photfit2m_general_sub 				+
			|restore=&"MAIN_PROC_NAME"_general.par 	+
		 	 save=&"MAIN_PROC_NAME"_general.par|
	else
	   write " ************************************************"
	   write " "
	   write " This program works only when run from tutor mode"
           write " of other programs."
	   write " "
	   write " ************************************************"
	end-if

end-proc

.TITLE
VICAR sub-menu &"MAIN_PROC_NAME"_GENERAL

.HELP
PURPOSE:
This menu point is dedicated to input general parameters for the program such 
as the names of input photometric catalog IBIS file, the type of 
photometric funtion, the fit conditions and the kind of outputs.

NOTE : The parameter PHO_FUNC is in need to select the photometric function :  
When returning to the highest level of the menu you will see that the second 
selection point has been changed according to your input of PHO_FUNC in this 
menu.


.PAGE
Programmer:

Friedel Oschuetz
Institute of Planetary Exploration
DLR
12484 Berlin (FRG)


.LEVEL1

.VARIABLE inp
  Input IBIS files, containing 
  the points generated by PHOTOM

.VARI PHO_FUNC
Photometric function type

.VARIABLE CLASS_ID
Class-id

.VARIABLE NORM
Causes subroutine Metropolis 
to renormalize itself.

.VARIABLE RERUN
Number of rerun of metropolis

.VARIABLE MAXITER
Specifies the total number of 
successful iterations.

.VARIABLE NUMTEN
Specifies the number of successful 
iterations which must be 
accumulated before the width of 
the solution generating  probability
function drops by a factor of ten.

.VARIABLE PERCENT
The minimum acceptable # of points
with residuals below tolerance.

.VARIABLE TOLERANC
The I/F residual tolerance.

!.VARIABLE PLOT
!Keyword for plotting the residuals.
!NOPLOT deactivates plotting.

.VARIABLE PRINT
Keyword for printing 
of the IBIS input files.
NOPRINT deactivates printing.

!.VARIABLE DISP
!Disp on image display?

.VARIABLE METROP
Causes subroutine Metropolis to 
list the iteration progress.

.LEVEL2

.VARI inp
File names of the input input IBIS photometric catalog files.
There are 18 columns in this file. 
All are not used exept for columns # 11, 12, 13, and 16. 
These columns contain :
	column # 11 = incidence angle (degrees),
	column # 12 = emission angle (degrees),
	column # 13 = phase angle (degrees)
	column # 16 = I/F reflectance values as computed from the photometric 
		      function.
	column # 18 = Class-ID

.VARI PHO_FUNC
Photometric function :

This parameter selects the menu point for input the photometry task:
   1. to run the program without using a photometric function, you have 
      to select "NONE"'
   2. to read in the photometric function and its associated parameters
      from a photometric parameter file, you have to select "PAR_FILE" and
   3. to put in the parameter by yourself from the tutor mode, 
      you have to select the desired photometric function.

When returning to the highest level of the menu (i.e. the MDF-file) you will
see that the third selection point has been changed according to your input of
PHO_FUNC in this menu.

.VARIABLE CLASS_ID
The class_id nummerates the photometric functions. For using different fotometric functions or parameter sets.

.VARIABLE NORM
Causes subroutine Metropolis to renormalize itself by recomputing the
Boltzmann coefficient. NORM=n causes renormalization each n successful
iterations. 

.VARIABLE RERUN
Number of rerun of metropolis.

.VARIABLE MAXITER
Specifies the number of successful iterations which Metropolis will
perform before ceasing in it's hunt for the coefficient values.
Usefully is for MAXITER is:
for MINNAERT 5000
for VEVERKA  20000
for HAPKE_* 20000
for HAPKE_* 20000 

.VARIABLE NUMTEN
Specifies the number of successful iterations which must be 
accumulated before the width of the solution generating  probability
function drops by a factor of ten. If for example MAXITER/NUMTEN
is 4.0 then the initial range specified by the MTEMP or HTEMP...
parameter (the starting temperature) is reduced by 4.0 orders
of magnitude (10000:1) by the time the iteration process has
ceased. 
Default for NUMTEN is: MAXITER/4

.VARIABLE PERCENT
The minimum acceptable # of points
with residuals below tolerance.
The percent and tolerance keywords permit a solution that is found to
consist of a subset of all of the data points. If there are more than
percent of the points with I/F residuals below tolerance then the
remainder of the points can be ignored if they exceed tolerance.
If there are fewer than percent points with residuals below
tolerance then all of the points will be considered.

.VARIABLE TOLERANC
The I/F residual tolerance.
The percent and tolerance keywords permit a solution that is found to
consist of a subset of all of the data points. If there are more than
percent of the points with I/F residuals below tolerance then the
remainder of the points can be ignored if they exceed tolerance.
If there are fewer than percent points with residuals below
tolerance then all of the points will be considered.

!.VARIABLE PLOT
!Keyword for plotting the residuals of the 
!fits on the input tiepoints.
!The default is to plot the residuals.
!NOPLOT deactivates printing of residuals.
!See PRINTRON to select device.

.VARIABLE PRINT
Keyword for printing of the IBIS input files.
NOPRINT deactivates printing of IBIS input file.

!.VARIABLE DISP
!Keyword to plot on image display.
!DISP       : Plot display.
!NODISP     : Without generted graphs.
!WAITNOMORE : All graph images will be save in the PostScript image file.

.VARIABLE METROP
Causes subroutine Metropolis to list the iteration progress as it
converges upon the solution. METROP=n causes a printout each
n successful iterations. 


.end
