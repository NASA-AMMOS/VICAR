CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C Compute LUT for automatic linear stretch.
C
      SUBROUTINE ASTRETCH(HIST,NPTS,lut,nmin,nmax)
      IMPLICIT NONE
      INTEGER*4 NPTS		!number of pixels in histogram
      INTEGER*4 NMIN,NMAX	!linear stretch limits (returned)
      INTEGER*4 HIST(-32768:32767)
      INTEGER*2 LUT(-32768:32767)

      COMMON/C3/INMIN,INMAX,DNMIN,DNMAX
      INTEGER*4 INMIN,INMAX,DNMIN,DNMAX

      REAL*4 A,B,RTRUNC
      INTEGER*4 IDN

      CALL ASTRETCH_LIMITS(HIST,NPTS,nmin,nmax)
C     ....Compute stretch table
      A = FLOAT(DNMAX-DNMIN)/FLOAT(NMAX-NMIN)
      B = -A*NMIN+DNMIN
      DO IDN=INMIN,INMAX
         LUT(IDN) = RTRUNC(A*IDN+B)
      ENDDO
      RETURN
      END

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C Compute linear stretch limits from histogram.
C
      SUBROUTINE ASTRETCH_LIMITS(HIST,NPTS,nmin,nmax)
      IMPLICIT NONE
      INTEGER*4 NPTS		!number of pixels in histogram
      INTEGER*4 NMIN,NMAX	!linear stretch limits (returned)
      INTEGER*4 HIST(-32768:32767)

      COMMON/C3/INMIN,INMAX,DNMIN,DNMAX
      INTEGER*4 INMIN,INMAX,DNMIN,DNMAX

      REAL*4 RPARM(2),LPERC,HPERC
      INTEGER*4 ICOUNT,IDEF,NLEV
      CHARACTER*80 PRT
  101 FORMAT('Percent saturation at low end=',F6.2,' at high end=',F6.2)

      CALL XVPARM('PERCENT',RPARM,ICOUNT,IDEF,1)
      LPERC = RPARM(1)/2.
      HPERC = LPERC
      CALL XVPARM('LPERCENT',RPARM,ICOUNT,IDEF,1)
      IF (IDEF.EQ.0) LPERC=RPARM(1)
      CALL XVPARM('HPERCENT',RPARM,ICOUNT,IDEF,1)
      IF (IDEF.EQ.0) HPERC=RPARM(1)
      WRITE (PRT,101) LPERC,HPERC
      CALL XVMESSAGE(PRT,' ')

      NLEV = INMAX - INMIN + 1
      CALL ASTRCH(HIST(INMIN),nmin,nmax,LPERC,HPERC,NLEV)
      NMIN = NMIN + INMIN
      NMAX = NMAX + INMIN
      IF (NMIN.EQ.NMAX) NMAX= NMAX+1
      RETURN
      END
