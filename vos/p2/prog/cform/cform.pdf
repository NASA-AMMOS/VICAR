process help=*
!
! FILE NAMES
PARM INP TYPE=STRING COUNT=1
PARM OUT TYPE=STRING COUNT=1
!
! SIZE FIELD
PARM SIZE TYPE=INTEGER COUNT=4 DEFAULT=(1,1,0,0)
PARM SL TYPE=INTEGER DEFAULT=1
PARM SS TYPE=INTEGER DEFAULT=1
PARM NL TYPE=INTEGER DEFAULT=0
PARM NS TYPE=INTEGER DEFAULT=0
!
! INPUT FILE PARAMETER
PARM IRANGE TYPE=REAL  COUNT=0:2  DEFAULT=--
!
! OUTPUT FILE PARAMETERS
PARM OFORM TYPE=KEYWORD VALID=(BYTE,HALF,FULL,REAL,REAL8,DOUB,COMPLEX)+
 COUNT=0:1 DEFAULT=--
PARM ORANGE TYPE=REAL  COUNT=0:2  DEFAULT=--
PARM BINARY TYPE=KEYWORD VALID=BINARY COUNT=0:1 DEFAULT=--
!
! SLOPE AND OFFSET
PARM SO TYPE=REAL COUNT=0:2 DEFAULT=--

!# annot function="Vicar Data Conversion"
!# annot keywords=(image,linear,transform,buffer,halfwords,stretch)

END-PROC
!
! HELP TEXT
.TITLE
Converts images between data types with optional scaling
.HELP
PURPOSE
  CFORM is a VICAR applications program which converts images between any
  of the recognized data formats and performs linear transformations
  on images.

EXECUTION

  CFORM  INP  OUT  PARAMS

  where INP and OUT are input and output file parameters and PARAMS are
  other parameter that are described in Tutor mode.

METHOD

  The program reads each image line into a buffer and treats the contents
  of the buffer as data of type implied by the label.  Byte data are read
  in as halfwords.  A simple linear function (stretch) is then applied to 
  these data and they are written out in the output format specified.

  If no format code is specified for OUT the following action is taken:
    1.  If input format is BYTE, then output format is HALF.
    2.  If input format is not BYTE, then output format is BYTE.
.page
 COMPLEX DATA:

  The linear relation between IN and OUT must be qualified for complex
  data according to the following table:

       INPUT        OUTPUT       Transformation Type
      Format        Format

      non-complex   complex       OUT = (IN*SL+OF) + 0.0i
      complex       non-complex   OUT = |IN| * SL + OF
      complex       complex       OUT = IN * SL + OF
.page
EXAMPLES

  CFORM A B (1,1,10,20) IRANGE=(10,20) ORANGE=(-20,2000) OFORM=FULL
    Data set B is fullword integer where the values have been scaled
    linearly such that 10 -> -20 and 20 -> 2000.  Only the first 10
    lines and 20 samples of A are passed to B.

  CFORM A B OFORM=HALF SO=(2,0)
    Data set B's format is a halfword integer, with each input value
    multiplied by 2.
.page
RESTRICTIONS

 The maximum permitted line length (bytes per line) is 60,000 for BYTE
 format and 120,000 otherwise.
.page
COGNIZANT PROGRAMMER

Written by: J. J. Lorre    18 April 1980

REVISIONS:

  1983-06 Dan Stanfill - Converted to VAX.
  1985-02 L. W. Kamp - Deleted FORMAT parameter, check for overflow in real-to-integer conversion, fixed test file.
  1988-01 Florance Moss - Put conversion factors into the label.
  2018-03-29 W. L. Bunch - Added dead code to work around gfortran optimizer bug. When compiling in 32-bits,
             the optimizer would prevent certain loops from executing. Here's an example of the dead code:

     C        This dead code (NSO is always > 0) is inserted to prevent the
     C        optimizer from breaking the incrementing of J.
     C        Current gfortran version is 4.8.5-11.el7.
     C        When gfortran is updated, this can be removed to test for a fix.
     C        Run tstcform.pdf and compare output with tstcform.log.
              if (NSO.lt.0) then
                 write(*,*) "j=",j
              endif   

Current Cognizant Programmer:  W. L. Bunch

.LEVEL1
.VARI INP
Input file name
.VARI OUT
Output file name
.VARI SIZE
Standard VICAR size field
.VARI SL
Starting line
.vari SS
Starting sample
.vari NL
Number of lines
.vari NS
Number of samples
.VARI IRANGE
Pair of numbers for performing
stretch.  ORANGE must also be
specified.
.VARI OFORM
Output storage format.
.VARI ORANGE
Output range values for stretch.
(See IRANGE.)
.VARI BINARY
Specifies that binary labels
must be copied to the output
.VARI SO
(Slope,offset) for linear
transformations.
.LEVEL2
.VARI INP
 Input file name
.VARI OUT
 Output file name
.VARI SIZE
The size field is specified with four arguments,
      SIZE=(SL,SS,NL,NS)
where:
SL is the starting line of the input file to be read.
SS is the starting sample to be read of each input line.
NL is the number of lines to be read.
NS is the number of samples to be read for each line.
For example, SIZE=(1,1,30,20)
would read the first 30 lines and 20 samples of the input picture.

These parameters can also be specified separately using SL, SS, NL,
and NS.
.VARI OFORM
OFORM (Keyword) specifies the format to which the input is to be converted.
The following are the valid values:

         BYTE              Unsigned 8-bit binary integer.
         HALF              Signed 16-bit binary integer.
         FULL              Signed 32-bit binary integer.
         REAL              VAX single precision floating point (REAL*4).
         DOUB or REAL8     VAX double precision floating point (REAL*8).
         COMPLEX           Two single precision floating point values
                           representing the real and imaginary parts of
                           a complex number (COMPLEX*8).
.VARI SO
     SO          SO is a keyword  followed by  two  values (real or
                 integer)  which specify  the slope (SL) and offset
                 (OF) of the  transformation  to be applied between
                 the input and output data sets in the sense:
                             OUT = IN * SL + OF
                 The default is SO=(1.0,0.0).
.VARI IRANGE
IRANGE  is a  keyword followed  by two values which specify two values
in the input  which  map to two values in the output (ORANGE must also 
be specified).  IRANGE and ORANGE values are converted into equivalent
SO values by "CFORM".  If SO is specified, then IRANGE and ORANGE are ignored.
If none of SO, IRANGE or ORANGE are specified, the default transformation
is performed:  SO=(1.0,0.0).
.VARI ORANGE
ORANGE is a keyword followed by two values which are used in conjunction
with the two values specified by IRANGE in order to determine a slope and
offset for a linear transformation.   See IRANGE.
.VARI BINARY
Specifies that any binary labels and prefixes in the input file must be 
copied to the output file.
.END
