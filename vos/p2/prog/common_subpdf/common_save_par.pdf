procedure option=selftutor help=*

!-----------------------------------------------------------------------------
! COMMON_SAVE_PAR.PDF
!
! This PDF-file was developed in order to provide various sub-PDF's which 
! can be used by a menu-driven PDF (i.e. a so-called MDF).
! This sub-PDF specifies an entry point in Menu where the user can specify 
! a name for the TAE-parameter file which is generated by the
! program (the default name in VICAR programs: LAST.PAR). This is useful 
! because in a Menu there is no 'save'-command to save a parameter file with 
! a user-specified name (e.g. "save proc_name.par").
!-----------------------------------------------------------------------------



	parm SAVE_PAR_temp	type=(string,40) count=0:1 default=--
	parm SAVE_PAR	type=(string,40) count=0:1 default=last.par
	parm MAIN_PROC_NAME string

	PARMSET name=common_save_sub help=*

		parm SAVE_PAR	type=(string,40) count=1 default=last.par

	END-PROC

body

	if (_tutor=1)

	  	restore-parm common_save_par.par
	  	restore-parm common_proc_name.par


		if (SAVE_PAR="last.par")
	 		let SAVE_PAR_temp="&MAIN_PROC_NAME"//".par"
		else
			let SAVE_PAR_temp="&SAVE_PAR"
	  	end-if

	   	tutor common_save_sub 					   +
		   |restore=common_save_par.par, save=common_save_par.par| +
		   SAVE_PAR=&SAVE_PAR_temp

	   	return


	else
	   write " "
	   write "*********************************************************"
	   write " "
	   write " This program is only intended to be run "
	   write " in Tutor Mode. "
	   write " "
	   write "*********************************************************"
	   write " "
	end-if

!# annot function="VICAR Utilities"
!# annot keywords=("sub-PDF",Menu,"TAE-parameter","LAST.PAR")
end-proc
.Title
Various sub-PDFs which can be used by menu-driven PDFs
.help
This sub-PDF specifies an entry point in Menu where the user can specify 
a name for the TAE-parameter file which is generated by the program (the 
default name in VICAR programs: LAST.PAR). This is useful because in a Menu 
there is no 'save'-command to save a parameter file with a user-specified name 
(e.g. "save proc_name.par").

EXECUTION :

USER ACTION				RESULT

don't call this menu point		last.par

exit this menu point with 'exit'	last.par

exit this menu point with 'run'		the user-specified name or the 'name 
					of the application procedure .par' as 
					it is given by the parameter 'save_par'


Programmer:

Friedel Oschuetz
Institute of Planetary Exploration
DLR
12484 Berlin (FRG)
.level1
.var save_par
file name for 'par-file'
.level2
.var save_par
This is the name of the file where all the parameters used in the final
execution of the application program are being stored 
(similarly to "last.par").
.end
