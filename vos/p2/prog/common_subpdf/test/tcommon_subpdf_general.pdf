procedure option=selftutor help=*

!-----------------------------------------------------------------------------
! TCOMMON_SUBPDF_GENERAL.PDF
!
! This is the PDF for the first menu point of the MDF file.
! In this PDF file general parameters are defined like the names of 
! input/output files, the name of the second entry point, and so on.
!
!-----------------------------------------------------------------------------

	!**********************************************************************
	! The global variables TCOMMON_FUNC, TMAIN_PROC_NAME (and $MENUS) will  
	! be used in this procedure, so they have to be declared here.
	! The global TCOMMON_FUNC (the desired function) will be
	! used to change the second menu point of the MDF file (see above)
	! according to the input to FUNCTION in this PDF.
	! The global $MENUS keeps the active stack of Menu Definition File
	! names. $MENUS(1) is the root menu name, $MENUS(2) the menu selected
	! from the root menu, and so on. The current menu stack can be
	! displayed with "DISPLAY $MENUS". Since there is only one MDF in
	! this demo, it doesn't make too much sense to have that global
	! included here. It is just referenced for completeness.
	!**********************************************************************

	refgbl TCOMMON_FUNC 
	refgbl TMAIN_PROC_NAME 
	refgbl $menus

	parm MAIN_PROC_NAME string

	procedure name=tcommon_subpdf_general_sub help=*

		! dummy inputs :

		parm inp	type=(string,32) count=0:1 default=--
		parm out	type=(string,32) count=0:1 default=--

		! functions :

		parm FUNCTION type=(string,32) count=1 	+
			valid = ( 			+
				FUNC1,			+
				FUNC2,			+
				FUNC3			+
						)	+
						 default="&TCOMMON_FUNC"

		! other parameters :

    		parm INC_ANG	real count=0:1 +
					default=--
    		parm EM_ANG 	real count=0:1 +
					default=--
    		parm PHAS_ANG 	real count=0:1 +
					default=--



	body

	!*******************************************************************
	! "tcommon_global.pdf" is being executed in the following line. 
	! In this PDF, two global are defined (TCOMMON_FUNC, 
	! TMAIN_PROC_NAME)
	!*******************************************************************
 
	tcommon_global TCOMMON_FUNC="&FUNCTION"

	end-proc
body

	if (_tutor=1)

 	   tutor tcommon_subpdf_general_sub 				+
			|restore=&"TMAIN_PROC_NAME"_general.par 	+
		 	 save=&"TMAIN_PROC_NAME"_general.par|
	else
	   write " ************************************************"
	   write " "
	   write " This program works only when run from tutor mode"
           write " of other programs."
	   write " "
	   write " ************************************************"
	end-if

end-proc

.TITLE
VICAR sub-menu TCOMMON_SUBPDF_GENERAL

.HELP
PURPOSE:
This menu point is dedicated to input general parameters for the program such 
as the names of input/output images the illumination conditions, and so on.

NOTE : The parameter FUNCTION is need to change the second entry point of 
       the menu :  When returning to the highest level of the menu 
      (i.e. the &"TMAIN_PROC_NAME".MDF-file) you will see that the second 
      selection point has been changed according to your input of FUNCTION in 
      this menu.


.PAGE
Programmer:

Friedel Oschuetz
Institute of Planetary Exploration
DLR
12484 Berlin (FRG)

HISTORY:
original version	F. Oschuetz DLR



.LEVEL1

.VARI inp
Input image

.VARI out
Output image

.VARI FUNCTION
Function --> second menu point

.VARI INC_ANG
incidence angle

.VARI EM_ANG
emission angle

.VARI PHAS_ANG
phase angle


.LEVEL2

.VARI inp
File name of the input image (dummy).

.VARI out
File name for the output image (dummy).

.VARI FUNCTION
The parameter FUNTION is need to change the second entry point of 
the menu :  When returning to the highest level of the menu 
(i.e. the &"TMAIN_PROC_NAME".MDF-file) you will see that the second 
selection point has been changed according to your input of FUNCTION in 
this menu.

.VARI INC_ANG
Incidence angle in degree (dummy general parameter).

.VARI EM_ANG
Emission angle in degree (dummy general parameter).

.VARI PHAS_ANG
Phase angle in degree (dummy general parameter).

.end
