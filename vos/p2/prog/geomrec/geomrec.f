C
      INCLUDE 'VICMAIN_FOR'
C
      SUBROUTINE MAIN44
C
C----------------------------------------------------------------------
C
C        1984   CONVERT FROM IBM VICAR1 TO VICAR1*
C  4 FEB 1985   CONVERT TO VICAR2
C 31 OCT 1994   CONVERT FOR PORTING
C
C----------------------------------------------------------------------
C
C TRANSFORMS RANGE MEASUREMENT TO DISTANCE ON SURFACE
C (TRANSFORMS FROM SLANT RANGE TO GROUND RANGE)
C
C OUTPUT SCALE IS EQUAL TO AZIMUTH SCALE
C PARAMETER 1 - SENSOR HEIGHT (METERS) SPECIFIED AS AN INTEGER
C PARAMETER 2 - RANGE SAMPLE SPACING (MICRONS) SPECIFIED AS AN INTEGER
C PARAMETER 3 - AZIMUTH SAMP SPACING (MICRONS) SPECIFIED AS AN INTEGER
C PARAMETER 4 - RANGE SCALE (METERS/MICRON ON FILM) FLOATING POINT
C PARAMETER 5 - AZIMUTH SCALE (METERS/MICRON ON FILM) FLOATING POINT
C PARAMETER 6 - ANGLE FROM VERTICAL OF 1ST RETURN (DEGREES) FLOAT. PT.
C
      COMMON / C1 / INBUF, OBUF
      COMMON / C2 / INS1, INS2, F, G  
      INTEGER*2 INBUF(20000), OBUF(20000)
      INTEGER   INS1(20000), INS2(20000)
      INTEGER   INUNIT, OUTUNIT, STATUS, ICNT
      REAL*4    F(20000), G(20000)
      CHARACTER*61  MES02
      CHARACTER*50  MES03, MES13
C
C
C   PROCESS THE PARAMETERS
C
      CALL IFMESSAGE('GEOMREC version 31-OCT-94')
      CALL XVUNIT(INUNIT,'INP',1,STATUS,' ')
      CALL XVOPEN(INUNIT,STATUS,'I_FORMAT','BYTE','U_FORMAT','HALF',
     +   'IO_ACT','SA','OPEN_ACT','SA',' ')
C
      CALL XVSIZE(ISL,ISS,NL,NS,INLI,INSI)
      CALL XVP('HEIGHT',IHGT,ICNT)
      CALL XVP('RSSPACE',IRSS,ICNT)
      CALL XVP('ASSPACE',IASS,ICNT)
      CALL XVP('RSCALE',RSCAL,ICNT)
      CALL XVP('ASCALE',ASCAL,ICNT)
      CALL XVP('THETA',ANGLE,ICNT)
C
C      OPEN THE OUTPUT FILE
C
      CALL XVUNIT(OUTUNIT,'OUT',1,STATUS,' ')
      CALL XVOPEN(OUTUNIT,STATUS,'OP','WRITE','O_FORMAT','BYTE',
     +        'U_FORMAT','HALF','IO_ACT','SA','OPEN_ACT','SA',' ')
C
      MES02 = '   INPUT   HEIGHT(PIXEL AND FEET)  = '
      MES03 = '           RANGE SAMP SPACING(MIC) =  '
      MES13 = '  OUTPUT NUMBER OF SAMPLES         =  '
      THETA = ANGLE*(3.1415/180.)
      RSS = FLOAT(IRSS)
      ASS = FLOAT(IASS)
      CONE = 1./(RSS*RSCAL)
      CTWO = 1./(ASS*ASCAL)
      RRES = RSS*RSCAL
      ARES = ASS*ASCAL
      HGT = FLOAT(IHGT)
      HGTFT = HGT * 3.28084
      ONETWO = CONE/CTWO
      HGTM = FLOAT(IHGT)*CONE
      HMS = HGTM*HGTM
      OFFSET = HGT*TAN(THETA)
      SNI = FLOAT(NS-1)
      Z = RRES*SNI
      ZZ = (Z+HGT/COS(THETA))**2 - HGT**2
      SNO = CTWO*(SQRT(ZZ)-OFFSET) + 1.0
      NSO = IFIX(SNO)
C
      WRITE(MES02(42:48),'(F7.1)') HGTM
      WRITE(MES02(52:61),'(F10.1)') HGTFT
      WRITE(MES03(42:48),'(F7.1)') RSS
      WRITE(MES13(41:47),'(I7)') NSO
      CALL XVMESSAGE(MES02,' ')
      CALL XVMESSAGE(MES03,' ')
      CALL XVMESSAGE(MES13,' ')
C
C     INITIALIZE ALL ARRAYS TO ZERO
C
      CALL ZIA(INS1,NSO)
      CALL ZIA(INS2,NSO)
      CALL ZIA(F,NSO)
      CALL ZIA(G,NSO)
      DO 30  KOSAMP= 1, NSO
         ZZ = (ARES*(KOSAMP-1)+OFFSET)**2 + HGT**2
         XINS = CONE*(SQRT(ZZ)-HGT/COS(THETA))
         INS = IFIX(XINS)
         FINS = XINS - FLOAT(INS)
         INS = INS + 2
         IF(INS .GT. NS) GO TO 30
         INS1(KOSAMP) = INS - 1
         INS2(KOSAMP) = INS
         F(KOSAMP) = 1.0 - FINS
         G(KOSAMP) = FINS
   30 CONTINUE
C
C  AI LOOP
C
      DO 100  IOLINE = 1,NL
         CALL XVREAD(INUNIT,INBUF,STATUS,'NSAMPS',NS,'SAMP',ISS,
     +               'LINE',IOLINE,' ')
         DO  90  IOSAMP = 1,NSO
            IN1 = INS1(IOSAMP)
            IN2 = IN1 + 1
            FF = F(IOSAMP)
            GG = 1.0 - FF
            FFF = (INBUF(IN1))
            GGG = (INBUF(IN2))
            XOUT = (FF*FFF) + (GG*GGG)
            IOUT = IFIX( 0.5 + XOUT)
            OBUF(IOSAMP) = IOUT
   90    CONTINUE
C
C  WRITE OUTPUT LINE
C
         CALL XVWRIT(OUTUNIT,OBUF,STATUS,'NSAMPS',NS,' ')
  100 CONTINUE
      CALL XVCLOSE(INUNIT,STATUS,' ')
      CALL XVCLOSE(OUTUNIT,STATUS,' ')
      RETURN
      END


