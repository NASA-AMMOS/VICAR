C/*   22 JULY 80   ...GMY...    INITIAL RELEASE */
c     Sept. 1992   ...WPL... Ported for UNIX Conversion
c
      SUBROUTINE MOMATI(OAL,OAS,SSL,SSS,SCALE,FL,SCLO,SCLA,ANGN,RANGE,
     &A,RS)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 A(3,3),RS(3)
c
C THIS ROUTINE COMPUTES THE PLANET TO CAMERA ROTATION MATRIX (A) AND THE
C VECTOR FROM PLANET CENTER TO SPACECRAFT (RS), EXPRESSED IN THE PLANET
C SYSTEM. A VECTOR V IN THE PLANET SYSTEM IS THEN RELATED TO THE SAME
C VECTOR VP IN THE CAMERA SYSTEM by
c
C          VP = A*V + RS   ,   Note  A is a 3x3  Matrix
c
C REQUIRED NAVIGATION INFORMATION ...
C  OAL,OAS=LINE,SAMPLE LOCATION OF OPTIC AXIS
C  SSL,SSS=LINE,SAMPLE LOCATION OF PLANET CENTER
C  SCLA=PLANETOCENTRIC LATITUDE OF S/C (DEG)
C  SCLO=WEST LONGITUDE OF S/C
C  ANGN=NORTH ANGLE AT OPTIC AXIS INTERCEPT POINT
C  RANGE=RANGE FROM S/C TO PLANET CENTER
C  SCALE=SCALE IN PIXELS/MM
C  FL=FOCAL LENGTH IN MM
C
      PI = 3.141592653589D0
      RAD = PI/180.D0
      CLA = DCOS(SCLA*RAD)
      SLA = DSIN(SCLA*RAD)
      CLON = DCOS(SCLO*RAD)
      SLON = DSIN(SCLO*RAD)
C
C          THIS SECTION COMPUTES ROTATIONS A AND B
C          FIRST COMPUT RS VECTOR IN CAMERA SYSTEM
c
      X = SSS - OAS
      Y = SSL - OAL
      Z = FL*SCALE
      D = DSQRT(X*X+Y*Y+Z*Z)
c
C          CHECK FOR SPECIAL CASES
c
      ICASE = 0
c
C          INITIALLY, NA=0, A=90, B=0 (CASE 1)
c
      CNA = 1.D0
      SNA = 0.D0
      CRA = 0.D0
      SRA = 1.D0
      IF(SCLA.LT.0.D0) SRA=-1.D0
      CRB = 1.D0
      SRB = 0.D0
      D1 = DSQRT(X*X+Y*Y)
      IF(DABS(SCLA).NE.90.D0) GOTO 3
      IF(D1.EQ.0.D0) GOTO 5
C
C          CASE 2  SCLA=90, (OAL,OAS).NE.(SSL,SSS)
c
      ICASE = 2
      CLON = -CLON
      SLON = -SLON
      CNA = Y/D1
      SNA = -X/D1
      IF(SCLA.GT.0.) GOTO 10
      CNA = -CNA
      SNA = -SNA
      GOTO 10
C
C
3     IF(ANGN.NE.-999.) GOTO 6
      IF(D1.EQ.0.D0) GOTO 5
c
C          CASE 3  SPIN AXIS PARALLEL TO OPTIC AXIS
c
      CNA = Y/D1
      SNA = -X/D1
      IF(SCLA.LT.0.) GOTO 4
      CNA = -CNA
      SNA = -SNA
4     CLA = DABS((-X*SNA+Y*CNA)/D)
      SLA = Z/D
      IF(SCLA.LT.0.) SLA=-SLA
      GOTO 15
C
C          CASE 1  SCLA=90, (OAL,OAS)=(SSL,SSS)
5     CLA = 0.D0
      SLA = -1.D0
      IF(SCLA.LT.0.) GOTO 15
      SLA = 1.D0
      CLON = -CLON
      SLON = -SLON
      GOTO 15
C
C          NORMAL CASE
c
6     CNA = DCOS(ANGN*RAD)
      SNA = DSIN(ANGN*RAD)
C          ROTATE ABOUT Z-AXIS SO THAT NORTH IS UP IN IMAGE
10    CX = (X*CNA + Y*SNA)/D
      CY = (-X*SNA+ Y*CNA)/D
      CZ = Z/D
c
C          ROTATE ABOUT X-AXIS SO THAT -Y AXIS IS PARALLEL TO SPIN AXIS.
c
      D = CY*CY + CZ*CZ
      G = D - SLA*SLA
      IF(G.LT.0.) G=0.
      G = DSQRT(G)
      CRA = (CY*SLA+CZ*G)/D
      SRA = (CZ*SLA-CY*G)/D
      IF(ICASE.NE.0) GOTO 15
      CZ = CZ*CRA - CY*SRA
      D = DSQRT(CX*CX+CZ*CZ)
      CRB = CZ/D
      SRB = CX/D
C
C          THIS SECTION COMPUTES RESULTANT ROTATION MATRIX
C          A MATRIX IS INITIALLY NORTH ANGLE ROTATION ABOUT Z-AXIS
c
C     changed by Roatsch, March 2000, was
c15    CALL ZIA(A,18)
15    Continue
      A(1,3) = 0.d0
      A(2,3) = 0.d0
      A(3,1) = 0.d0
      A(3,2) = 0.d0
      
c     continues original code      
      A(1,1) = CNA
      A(2,1) = SNA
      A(1,2) = -SNA
      A(2,2) = CNA
      A(3,3) = 1.D0
c
C          ROTATE ABOUT X-AXIS SO THAT -Y AXIS IS PARALLEL TO SPIN AXIS.
C          A = M1*A
      DO 20 I=1,3
      A2 = CRA*A(I,2) + SRA*A(I,3)
      A(I,3) = -SRA*A(I,2) + CRA*A(I,3)
20    A(I,2) = A2

c
C          ROTATE ABOUT Y-AXIS SO THAT PLANET CENTER LIES IN Y-Z PLANE.
C          A = M2*A
c
      DO 30 I=1,3
      A1 = CRB*A(I,1) - SRB*A(I,3)
      A(I,3) = SRB*A(I,1) + CRB*A(I,3)
30    A(I,1) = A1
C          INTERCHANGE COORDINATES SO THAT
C              XP = -Z, YP=X, ZP=-Y
      DO 40 I=1,3
      A1 = A(I,1)
      A2 = A(I,2)
      A(I,1) = -A(I,3)
      A(I,2) = A1
40    A(I,3) = -A2
c
C          ROTATE ABOUT Z-AXIS SO THAT X-AXIS POINTS TO LON 0, LAT 0.
C          A = M3*A
c
      DO 50 I=1,3
      A1 = CLON*A(I,1) + SLON*A(I,2)
      A(I,2) = -SLON*A(I,1) + CLON*A(I,2)
50    A(I,1) = A1
C
C          COMPUTE VECTOR FROM PLANET CENTER TO S/C, IN PLANET SYSTEM.
c
      RS(1) = RANGE*CLA*CLON
      RS(2) = -RANGE*CLA*SLON
      RS(3) = RANGE*SLA
c
      RETURN
      END
